//===== rAthena Script ======================================= 
//= Buwaya Cave
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Defeat Buwaya in his cave.
//= Part of the "Secret in the Woods" quest.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//============================================================ 

// Instance Creation :: buwaya_dun
//============================================================
ma_fild02,312,317,5	script	卫兵#buwaya_cave	4_MAL_SOLDIER,{
	if (BaseLevel < 130) {
		mes "[卫兵]";
		mes "基本等级低于 ^ff0000130级^000000";
		mes "是无法进入这个地方的.";
		mes "这里很危险. 请回吧.";
		close;
	}

	set .@party_id,getcharid(1);
	set .@md_name$,"宝箱巨鳄之巢";

	if (!.@party_id) {
		mes "[卫兵]";
		mes "宝箱鳄鱼非常危险.";
		mes "等你组织一支队伍后再回到这里";
		mes "进行探险吧.";
		close;
	}
	if (is_party_leader() == true) {
		mes "[卫兵]";
		mes "这里很危险. 请回吧.";
		mes "如果你在附近没有其他的事情, 还是快点离开吧.";
		next;
		while(1) {
			switch(select("- 为什么说这里很危险?:- 我是来猎杀宝箱鳄鱼的.:- 结束对话.")) {
			case 1:
				mes "[卫兵]";
				mes "最近一直接到附近有村民失踪的报告. ";
				mes "经过我们调查我们发现了这里, ";
				mes "并且发现了这条巨型鳄鱼. ";
				mes "我们有充分理由相信,";
				mes "村民的失踪原因就是它.";
				next;
				mes "[卫兵]";
				mes "已经派遣了士兵们追杀这条鳄鱼, ";
				mes "不过迄今为止还没有收到任何消息.";
				mes "我们只能耐心的等待.";
				next;
				mes "[卫兵]";
				mes "这条鳄鱼近期还在危害附近的村民";
				mes "冒险家, 召集一批英雄们,";
				mes "一起来消灭它把.";
				next;
				break;
			case 2:
				set .@playtime, checkquest(4229,PLAYTIME);
				if (.@playtime == -1) {
					// fall through
				} else if (.@playtime == 0 || .@playtime == 1) {
					mes "[卫兵]";
					mes "宝箱鳄鱼还藏匿在深处.";
					mes "就算你现在进去也不会碰到它. ";
					mes "还是晚点再来吧.";
					close;
				} else {
					erasequest 4229;
					// fall through
				}
				if (instance_create(.@md_name$) < 0) {
					mes "[卫兵]";
					mes "队伍名称为... "+getpartyname(.@party_id)+".";;
					mes "队长是... "+strcharinfo(0);
					mes "^0000ff"+.@md_name$+"^000000 现在不能被打开.";
					mes "请稍等.";
					close;
				}
				mes "[卫兵]";
				mes "我将打开通往 ^0000ff"+.@md_name$+"^000000的通道.";
				mes "祝您好运.";
				close;
			case 3:
				mes "[卫兵]";
				mes "这里很危险. 你不能进去.";
				close;
			}
		}
	} else {
		mes "[卫兵]";
		mes "让我和队长谈谈.";
		mes "请联系下你们的队长.";
		close;
	}
}

ma_fild02,315,323,0	script	洞穴入口#buwaya	WARPNPC,2,2,{
	end;
OnTouch:
	if (BaseLevel < 130) {
		mes "[卫兵]";
		mes "基本等级低于 ^130级^000000";
		mes "是禁止进入这个区域的.";
		mes "这里很危险. 请回吧.";
		close;
	}
	set .@playtime, checkquest(4229,PLAYTIME);
	if (.@playtime == -1) {
		// fall through
	} else if (.@playtime == 0 || .@playtime == 1) {
		mes "[卫兵]";
		mes "宝箱鳄鱼还藏匿在深处.";
		mes "就算你现在进去也不会碰到它. ";
		mes "还是晚点再来吧.";
		close;
	} else {
		erasequest 4229;
		// fall through
	}
	switch(select("- 进入.:- 返回.")) {
	case 1:
		switch(instance_enter("宝箱巨鳄之巢")) {
		case IE_OTHER:
			mes "[卫兵]";
			mes "时间还不到，";
			mes "等一会在尝试吧.";
			close;
		case IE_NOINSTANCE:
		case IE_NOMEMBER:
			mes "[卫兵]";
			mes "这地方很危险，不要进入比较好.";
			close;
		case IE_OK:
			mapannounce "ma_fild02",getpartyname(getcharid(1))+" 小队成员 "+strcharinfo(0)+" 开始了猎杀宝箱鳄鱼的冒险旅程.",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
			setquest 4229;
			//warp "1@ma_c",35,57;
			end;
		default:
			mes "- 不知是什么力量阻挡住了前进的路 -";
			close;
		}
	case 2:
		mes "[卫兵]";
		mes "你要珍惜自己的生命.";
		mes "行动之前请先仔细的规划.";
		close;
	}
}

// Instance Scripts :: buwaya_in
//============================================================
1@ma_c,33,112,0	script	#damage	139,7,7,{
	end;
OnInstanceInit:
	initnpctimer;
	disablenpc instance_npcname("#damage");
	end;
OnTimer1000:
	enablenpc instance_npcname("#damage");
	specialeffect EF_POISONHIT;
	end;
OnTimer2000:
	stopnpctimer;
	initnpctimer;
	disablenpc instance_npcname("#damage");
	end;
OnTouch:
	percentheal -10,-10;
	sc_start SC_BLEEDING,60000,0;
	sc_start SC_POISON,60000,0;
	end;
}

1@ma_c,29,110,5	script	被绑架的人#1	4_F_BARYO_OLD,{
	mes "[被绑架的村民]";
	mes "宝箱鳄鱼四处活动,";
	mes "并不断的捕捉新的村民.";
	mes "我们被它的关在这里, 感觉慢慢在被融化.";
	next;
	switch(select("- 让我离开这里!!:- 呃...无法逃脱?")) {
	case 1:
		mes "[被绑架的村民]";
		mes "我们的力量无法逃离这里, 不过鳄鱼有个弱点.";
		mes "看那边墙上.";
		mes "如果你狠砸, 鳄鱼会吃疼, 然后可能把你吐出去.";
		donpcevent instance_npcname("#box_mob_call")+"::OnEnable";
		close2;
		disablenpc instance_npcname("被绑架的人#1");
		end;
	case 2:
		mes "[被绑架的人]";
		mes "不对劲...";
		mes "在这之前, 你有没类似经历?";
		close;
	}
	end;
OnInstanceInit:
	disablenpc instance_npcname("被绑架的人#1");
	end;
OnEnable:
	enablenpc instance_npcname("被绑架的人#1");
	donpcevent instance_npcname("#box_mob_call")+"::OnDisable";
	end;
}

1@ma_c,36,110,5	script	被绑架的人#2	4_M_BARYO_OLD,{
	mes "[被绑架的村民]";
	mes "啊啊! 我希望我的力气能更大点...";
	mes "我知道它的弱点...";
	next;
	switch(select("- 告诉我.:- 你真知道?")) {
	case 1:
		mes "[被绑架的村民]";
		mes "你确定你能做到...?";
		mes "好吧, 仔细听好了,";
		mes "它的弱点就是...";
		next;
		mes "[被绑架的村民]";
		mes "...在我们身后的墙壁上.";
		sc_start SC_ATKPOTION,60000,45;
		sc_start SC_MATKPOTION,60000,45;
		disablenpc instance_npcname("被绑架的人#2");
		close;
	case 2:
		mes "[被绑架的村民]";
		mes "什么?!";
		mes "你无视我";
		mes "是因为我也被困在这里的原因?";
		close;
	}
	end;
OnInstanceInit:
	disablenpc instance_npcname("被绑架的人#2");
	end;
OnEnable:
	enablenpc instance_npcname("被绑架的人#2");
	end;
}

1@ma_c,3,3,0	script	#box_mob_call	HIDDEN_WARP_NPC,1,1,{
	end;
OnInstanceInit:
	setcell instance_mapname("1@ma_c"),30,118,35,118,cell_shootable,1; //custom
	disablenpc instance_npcname("#box_mob_call");
	end;
OnEnable:
	enablenpc instance_npcname("#box_mob_call");
	set .@label$, instance_npcname("#box_mob_call")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_c");
	monster .@map$,30,118,"宝箱鳄鱼的弱点",2333,1,.@label$;
	monster .@map$,35,118,"宝箱鳄鱼的弱点",2333,1,.@label$;
	end;
OnDisable:
	killmonster instance_mapname("1@ma_c"),instance_npcname("#box_mob_call")+"::OnMyMobDead";
	disablenpc instance_npcname("#box_mob_call");
	end;
OnMyMobDead:
	if (mobcount(instance_mapname("1@ma_c"),instance_npcname("#box_mob_call")+"::OnMyMobDead") < 1)
		donpcevent instance_npcname("#box_out")+"::OnEnable";
	end;
}

1@ma_c,33,118,0	script	#box_out	45,2,2,{
OnInstanceInit:
OnDisable:
	disablenpc instance_npcname("#box_out");
	end;
OnEnable:
	enablenpc instance_npcname("#box_out");
	end;
OnTouch:
	set .@x, rand(1,20) + 97;
	set .@y, rand(1,20) + 74;
	warp instance_mapname("1@ma_c"),.@x,.@y;
	end;
}

1@ma_c,97,74,0	script	#box_call	139,50,50,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#box_call");
	initnpctimer;
	end;
OnTimer30000:
	mapannounce instance_mapname("1@ma_c"),"宝箱鳄鱼 : 我要把你关进我的宝箱!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	// Should execute OnTimer33000, but client doesn't render the effect fast enough.
	for(set .@i,1; .@i<=9; set .@i,.@i+1)
		donpcevent instance_npcname("#yunobi"+.@i)+"::OnEnable";
	end;
OnTimer33000:
	donpcevent instance_npcname("#box_out")+"::OnDisable";
	donpcevent instance_npcname("#box_mob_call")+"::OnDisable";
	donpcevent instance_npcname("被绑架的人#1")+"::OnEnable";
	donpcevent instance_npcname("被绑架的人#2")+"::OnEnable";
	end;
OnTimer34000:
	enablenpc instance_npcname("#box_call");
	end;
OnTimer35000:
	stopnpctimer;
	initnpctimer;
	disablenpc instance_npcname("#box_call");
	end;
OnTouch_:
	specialeffect2 EF_GUIDEDATTACK;
	warp instance_mapname("1@ma_c"),33,112;
	end;
OnDisable:
	stopnpctimer;
	disablenpc instance_npcname("#box_call");
	end;
}

1@ma_c,97,74,0	script	#yunobi1	139,{
	end;
OnInstanceInit:
	hideonnpc instance_npcname(strnpcinfo(0));
	end;
OnEnable:
	specialeffect EF_MAPPILLAR2;
	end;
}
1@ma_c,97,94,0	duplicate(#yunobi1)	#yunobi2	139
1@ma_c,117,94,0	duplicate(#yunobi1)	#yunobi3	139
1@ma_c,117,74,0	duplicate(#yunobi1)	#yunobi4	139
1@ma_c,117,54,0	duplicate(#yunobi1)	#yunobi5	139
1@ma_c,97,54,0	duplicate(#yunobi1)	#yunobi6	139
1@ma_c,77,54,0	duplicate(#yunobi1)	#yunobi7	139
1@ma_c,77,74,0	duplicate(#yunobi1)	#yunobi8	139
1@ma_c,77,94,0	duplicate(#yunobi1)	#yunobi9	139

1@ma_c,1,1,0	script	#bunshin	139,{
	end;
OnInstanceInit:
	initnpctimer;
	end;
OnTimer58000:
	mapannounce instance_mapname("1@ma_c"),"宝箱鳄鱼 : 我受够了, 让我们走着瞧!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	end;
OnTimer61000:
	mapannounce instance_mapname("1@ma_c"),"宝箱鳄鱼 : 这是!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	end;
OnTimer62000:
	mapannounce instance_mapname("1@ma_c"),"宝箱鳄鱼 : 这是...我的!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	end;
OnTimer63000:
	mapannounce instance_mapname("1@ma_c"),"宝箱鳄鱼 : 这是...我的...致命弱点!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	end;
OnTimer64000:
	mapannounce instance_mapname("1@ma_c"),"宝箱鳄鱼 : 这是...我的...致命弱点... 攻击!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	end;
OnTimer65000:
	set .@label$, instance_npcname("#bunshin")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_c");
	areamonster .@map$,112,89,122,99,"宝箱鳄鱼",2332,1,.@label$;
	areamonster .@map$,112,49,122,59,"宝箱鳄鱼",2332,1,.@label$;
	areamonster .@map$,72,49,82,59,"宝箱鳄鱼",2332,1,.@label$;
	areamonster .@map$,72,89,82,99,"宝箱鳄鱼",2332,1,.@label$;
	end;
OnTimer66000:
	mapannounce instance_mapname("1@ma_c"),"宝箱鳄鱼 : 你怕了吗?",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	end;
OnTimer105000:
	killmonster instance_mapname("1@ma_c"),instance_npcname("#bunshin")+"::OnMyMobDead";
	stopnpctimer;
	initnpctimer;
	end;
OnMyMobDead:
	if (mobcount(instance_mapname("1@ma_c"),instance_npcname("#bunshin")+"::OnMyMobDead") < 1) {
		stopnpctimer;
		initnpctimer;
	}
	end;
OnDisable:
	stopnpctimer;
	killmonster instance_mapname("1@ma_c"),instance_npcname("#bunshin")+"::OnMyMobDead";
	disablenpc instance_npcname("#bunshin");
	end;
}

1@ma_c,2,2,0	script	#buwaya_con	139,{
	end;
OnInstanceInit:
	areamonster instance_mapname("1@ma_c"),90,67,104,81,"宝箱巨鳄",2319,1,instance_npcname("#buwaya_con")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_c");
	if (mobcount(.@map$,instance_npcname("#buwaya_con")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#box_call")+"::OnDisable";
		donpcevent instance_npcname("#bunshin")+"::OnDisable";
		donpcevent instance_npcname("#exit_mob")+"::OnDisable";
		donpcevent instance_npcname("#cave_out")+"::OnEnable";
		mapannounce .@map$,"卫兵 : 你干的太出色了. 请抓紧时间逃离这里!",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
	}
	end;
}

1@ma_c,3,3,0	script	#exit_mob	139,{
	end;
OnInstanceInit:
	initnpctimer;
	end;
OnTimer60000:
	set .@label$, instance_npcname("#exit_mob")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_c");
	if (mobcount(.@map$,.@label$) < 30)
		set .@amount,10;
	else
		set .@amount,1;
	areamonster .@map$,43,58,47,60,"水草",2331,.@amount,.@label$;
	areamonster .@map$,43,58,47,60,"蛋",2329,.@amount,.@label$;
	stopnpctimer;
	initnpctimer;
	end;
OnDisable:
	stopnpctimer;
	killmonster instance_mapname("1@ma_c"),instance_npcname("#exit_mob")+"::OnMyMobDead";
	disablenpc instance_npcname("#exit_mob");
	end;
OnMyMobDead:
	end;
}

1@ma_c,28,57,0	script	#cave_out	45,2,2,{
OnInstanceInit:
	disablenpc instance_npcname("#cave_out");
	end;
OnEnable:
	enablenpc instance_npcname("#cave_out");
	end;
OnTouch:
	mes "你是不是要出去?";
	next;
	if(select("- 是!:- 不, 我留在这里.") == 1)
		warp "ma_fild02",315,315;
	close;
}

1@ma_c,1,1,0	script	#buwaya_spawn_mobs	-1,{
OnInstanceInit:
	set .@map$, instance_mapname("1@ma_c");
	areamonster .@map$,73,81,93,101,"海草",2331,18;
	areamonster .@map$,110,97,116,103,"海草",2331,8;
	areamonster .@map$,59,63,63,67,"海草",2331,8;
	areamonster .@map$,73,55,77,59,"海草",2331,4;
	areamonster .@map$,103,69,107,73,"海草",2331,4;
	areamonster .@map$,108,45,122,63,"海草",2331,15;
	areamonster .@map$,73,81,93,101,"鳄鱼蛋",2329,10;
	areamonster .@map$,110,97,116,103,"鳄鱼蛋",2329,8;
	areamonster .@map$,59,63,63,67,"鳄鱼蛋",2329,4;
	areamonster .@map$,73,55,77,59,"鳄鱼蛋",2329,3;
	areamonster .@map$,103,69,107,73,"鳄鱼蛋",2329,3;
	areamonster .@map$,108,45,122,63,"鳄鱼蛋",2329,15;
	monster .@map$,0,0,"海草",2331,5;
	monster .@map$,0,0,"宝箱幼鳄",2330,5;
	end;
}
