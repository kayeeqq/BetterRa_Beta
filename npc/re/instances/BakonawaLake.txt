//===== rAthena Script ======================================= 
//= Bakonawa Lake
//===== By: ================================================== 
//= Euphy
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Defeat Bakonawa before he eats the moon.
//= Part of the "Bakonawa Extermination" quest.
//===== Additional Comments: ================================= 
//= 1.0 First version. [Euphy]
//= 1.1 Added GM management NPC. [Euphy]
//= 1.2 Added VIP features. [Euphy]
//============================================================ 

// Instance Creation :: ma_baku
//============================================================
ma_scene01,174,179,4	script	酸奶	541,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "您攜帶的物品种類太多, 請稍做處理.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "您的負重不足, 請稍做處理.";
		close;
	}
	if (BaseLevel < 140) {
		mes "[酸奶]";
		mes "嘿. 這地方對你來說太危險了. 你會喪命的. 立即离開這里回鎮上!";
		close;
	}
	if (malaya_bakona1 < 7 && malaya_bakona2 < 15) {
		mes "[酸奶]";
		mes "我們都會喪命在食月暴龍手上的. 啊, 我們必須殺了它, 但不幸的是我們都沒有做好准備.";
		close;
	}

	set .@party_id,getcharid(1);
	set .@md_name$, "食月暴龍湖泊";

	set .@baku_time, checkquest(12278,PLAYTIME);
	if (.@baku_time == -1) {
		if (checkquest(12279) == -1) setquest 12279;
	}
	if (!.@party_id) {
		mes "[酸奶]";
		mes "怎么你沒有其他伙伴, 難道你想一個人去和食月暴龍干架? 組織點人手再來吧.";
		close;
	}
	if (.@baku_time == -1) {
		if (is_party_leader() == true) {
			mes "[酸奶]";
			mes "你是獵殺暴龍隊伍的隊長嗎? 這里几乎沒有任何正常的路可以到那里, 我們會用繩索把你們吊下去, 沒問題吧?";
			next;
			set .@i, select("- 請制作吊繩.:- 我現在要下去了.:- 取消.");
		} else {
			mes "[酸奶]";
			mes "你們是來獵殺暴龍的. 隊長請出來和我一起編織下吊繩吧.";
			next;
			set .@i, select(":- 我現在要下去了.:- 取消.");
		}
		switch(.@i) {
		case 1:
			if (instance_create(.@md_name$) < 0) {
				mes "[酸奶]";
				mes "呃, 我不小心讓它掉下去了. 我必須做個新的.";
				close;
			}
			mes "[酸奶]";
			mes "我正在做新繩子呢, 你得等我做好了才能去了.";

			close;
		case 2:
			switch(instance_enter(.@md_name$)) {
			case IE_OTHER:
				mes "[Taho]";
				mes "An unknown error occurred.";
				close;
			case IE_NOINSTANCE:
				mes "[Taho]";
				mes "The rope hasn't been weaved yet. Wait a little.";
				close;
			case IE_NOMEMBER:
				mes "[Taho]";
				mes "Civilians cannot enter.";
				close;
			case IE_OK:
				mapannounce "ma_scene01",""+strcharinfo(0)+" 率領小隊 "+getpartyname(.@party_id)+" 進入了副本, "+.@md_name$+".",bc_map,"0x00ff99"; //FW_NORMAL 12 0 0
				setquest 12278;
				//warp "1@ma_b",64,51;
				close;
			}
		case 3:
			close;
		}
	} else if (.@baku_time == 0 || .@baku_time == 1) {
		mes "[酸奶]";
		mes "這里唯一能用的交通工具吊繩, 已經斷掉了. 我們正在重做并清理附近的戰場, 你們請稍候再來.";
		close;
	} else if (.@baku_time == 2) {
		mes "[酸奶]";
		mes "附近全部清掃干淨了. 等吊繩做好你們就能夠再次進入.";
		erasequest 12278;
		close;
	}
}

sec_in02,26,26,4	script	食月暴龍湖泊	541,{
	callfunc "F_GM_NPC";
	mes "[酸奶]";
	mes "我叫酸奶.";
	if (callfunc("F_GM_NPC",1854,0) == 1) {
		erasequest 12278;
		erasequest 12279;
		set malaya_bakona1,7;
		set malaya_bakona2,15;
	}
	close;
}

// Instance Scripts :: in_baku01
//============================================================
1@ma_b,62,52,4	script	酸奶#nf	541,{
	mes "[酸奶]";
	mes "嘿嘿, 我也要加入捕獵隊伍, 不過我只會在你們后面指揮不會沖鋒陷陣的.";
	next;
	mes "[酸奶]";
	mes "暴龍有時會變的很無敵. 當碰到這种情況時, 仔細听我的指導. 只要相信我, 一切都會好.";
	next;
	if (is_party_leader() == true) {
		mes "[酸奶]";
		mes "現在我會把暴龍給弄出湖水.";
		next;
		switch(select("- 等等! 我還沒准備好!:- 上吧!")) {
		case 1:
			mes "[酸奶]";
			mes "噢... 好吧... 准備好就通知我.";
			close;
		case 2:
			mes "[酸奶]";
			mes "一會我會洒一些 Albopal 到湖水里.";
			next;
			mes "[酸奶]";
			mes "一旦暴龍聞到這個味道, 它就會從湖里出來...";
			donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
			disablenpc instance_npcname("酸奶#nf");
			close;
		}
	} else {
		mes "[酸奶]";
		mes "我洒的 Alpopal 會讓暴龍變的很瘋狂. 暴龍的肉可是蘊含大量蛋白質的好東西.";
		close;
	}
}

1@ma_b,36,111,4	script	#Bakonawan1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan1");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan1");
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"酸奶: 我看到它浮出水面了! 攻擊!!!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	monster .@map$,78,81,"食月暴龍",2320,1,instance_npcname("#Bakonawan1")+"::OnMyMobDead"; //original: (78,83)
	initnpctimer;
	end;
OnFail:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	if (mobcount(.@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead") < 1) {
		donpcevent instance_npcname("#BakonawaDeadn1")+"::OnStart";
		killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan1");
	}
	end;
OnTimer1000:	callsub OnAnnounce,"10 minutes";
OnTimer60000:	callsub OnAnnounce,"9 minutes";
OnTimer120000:	callsub OnAnnounce,"8 minutes";
OnTimer180000:	callsub OnAnnounce,"7 minutes";
OnTimer240000:	callsub OnAnnounce,"6 minutes";
OnTimer300000:	callsub OnAnnounce,"5 minutes";
OnTimer360000:	callsub OnAnnounce,"4 minutes";
OnTimer420000:	callsub OnAnnounce,"3 minutes";
OnTimer480000:	callsub OnAnnounce,"2 minutes";
OnTimer540000:	callsub OnAnnounce,"1 minute";
OnTimer570000:
	mapannounce instance_mapname("1@ma_b"),"時間限制 : 30 秒",bc_map,"0xff4400"; //FW_NORMAL 18 0 0
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
OnTimer600000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"暴龍逃脫進入湖底了.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("酸奶#Fail")+"::OnStart";
	donpcevent instance_npcname("#Bakonawan1")+"::OnFail";
	killmonster .@map$,instance_npcname("#Bakonawan1")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan1");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"時間限制 : "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,78,81,0	script	#SummonPuppetsn1	139,4,5,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnStart:
	stopnpctimer;
	enablenpc instance_npcname("#SummonPuppetsn1");
	initnpctimer;
	end;
OnTouch:
	stopnpctimer;
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnTimer5000:
	setarray .@x[0],79,71,60,61,57,89,95,96,99;
	setarray .@y[0],71,72,80,90,99,73,82,90,99;
	set .@map$, instance_mapname("1@ma_b");
	for(set .@i,0; .@i<9; set .@i,.@i+1) {
		set .@rand, rand(1,10);
		if (.@rand > 7)
			monster .@map$,.@x[.@i],.@y[.@i],"食月暴龍的意志",2337,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
		else if (.@rand < 4)
			monster .@map$,.@x[.@i],.@y[.@i],"食月暴龍的意志",2343,1,instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
	}
	end;
OnTimer50000:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#SummonPuppetsn1")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#SummonPuppetsn1");
	end;
OnMyMobDead:
	end;
}

1@ma_b,1,5,4	script	#BakonawaDeadn1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#BakonawaDeadn1");
	end;
OnStart:
	enablenpc instance_npcname("#BakonawaDeadn1");
	initnpctimer;
	end;
OnTimer100:
	mapannounce instance_mapname("1@ma_b"),"酸奶: 很好! 暴龍躲在湖底. 它在嘗試吞掉月亮.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_b"),"酸奶: 我們必須制造噪音, 用上鍋子和銅鑼. 不能讓它專注在食月上面!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"酸奶: 不用去打它, 就算它浮在湖面上也不用管. 專心制造噪音!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer15000:
	mapannounce instance_mapname("1@ma_b"),"任務 - 在湖的左右兩側, 分別敲坏2個鍋子和2個銅鑼.",bc_map,"0xff3300"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#Bakonawan2")+"::OnStart";
	stopnpctimer;
	disablenpc instance_npcname("#BakonawaDeadn1");
	end;
}

1@ma_b,36,111,4	script	#Bakonawan2	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan2");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan2");
	set .@label$, instance_npcname("#Bakonawan2")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_b");
	monster .@map$,95,98,"鍋子",2328,1,.@label$;
	monster .@map$,60,98,"鍋子",2328,1,.@label$;
	monster .@map$,97,104,"銅鑼",2328,1,.@label$;
	monster .@map$,58,104,"銅鑼",2328,1,.@label$;
	donpcevent instance_npcname("#Bakonawan2-1")+"::OnStart";
	initnpctimer;
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead");
	if (.@mob_dead_num < 1) {
		donpcevent instance_npcname("#Bakonawan2-1")+"::OnEnd";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan2");
	} else
		mapannounce .@map$,"酸奶: Good! "+.@mob_dead_num+" left to go!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	 end;
OnTimer1000:	callsub OnAnnounce,"5 minutes",15,1;
OnTimer60000:	callsub OnAnnounce,"4 minutes",15,1;
OnTimer120000:	callsub OnAnnounce,"3 minutes",15,1;
OnTimer180000:	callsub OnAnnounce,"2 minutes",15,1;
OnTimer240000:	callsub OnAnnounce,"1 minute",15,1;
OnTimer270000:	callsub OnAnnounce,"30 seconds",15,0;
OnTimer280000:	callsub OnAnnounce,"20 seconds",15,0;
OnTimer290000:	callsub OnAnnounce,"10 seconds",15,0;
OnTimer295000:	callsub OnAnnounce,"5 seconds",16,0;
OnTimer296000:	callsub OnAnnounce,"4 seconds",17,0;
OnTimer297000:	callsub OnAnnounce,"3 seconds",18,0;
OnTimer298000:	callsub OnAnnounce,"2 seconds",19,0;
OnTimer299000:	callsub OnAnnounce,"1 second",20,0;
OnTimer300000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"食月暴龍逃進了湖底.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("酸奶#Fail")+"::OnStart";
	donpcevent instance_npcname("#Bakonawan2-1")+"::OnFail";
	killmonster .@map$,instance_npcname("#Bakonawan2")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan2");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"Time Limit: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL getarg(1) 0 0
	if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,36,111,4	script	#Bakonawan2-1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan2-1");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan2-1");
	monster instance_mapname("1@ma_b"),78,81,"憤怒的食月暴龍",2321,1,instance_npcname("#Bakonawan2-1")+"::OnMyMobDead"; //original: (78,93)
	end;
OnFail:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	end;
OnEnd:
	enablenpc instance_npcname("#Bakonawan2-1");
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan2-1")+"::OnMyMobDead";
	initnpctimer;
	end;
OnTimer1000:
	mapannounce instance_mapname("1@ma_b"),"酸奶: 它又逃進湖里了. 結束了嗎?",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer5000:
	mapannounce instance_mapname("1@ma_b"),"酸奶: 嗯! 有什么東西正在水下迅速上浮! 暴... 暴龍來了!!",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"任務 - 消滅變得狂暴的食月暴龍.",bc_map,"0xff3300"; //FW_NORMAL 15 0 0
	donpcevent instance_npcname("#Bakonawan3")+"::OnStart";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan2-1");
	end;
OnMyMobDead:
	end;
}

1@ma_b,36,111,4	script	#Bakonawan3	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan3");
	end;
OnStart:
	enablenpc instance_npcname("#Bakonawan3");
	initnpctimer;
	monster instance_mapname("1@ma_b"),78,81,"憤怒的食月暴龍",2322,1,instance_npcname("#Bakonawan3")+"::OnMyMobDead"; //original: (78,83)
	donpcevent instance_npcname("#Bakonawan3-1")+"::OnStart";
	end;
OnMyMobDead:
	set .@map$, instance_mapname("1@ma_b");
	set .@mob_dead_num, mobcount(.@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead");
	if (.@mob_dead_num < 1) {
		mapannounce .@map$,"酸奶: 我們逮到它了! 我看見它身上掉了一個盒子一樣的東西.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
		donpcevent instance_npcname("酸奶#Completed")+"::OnStart";
		donpcevent instance_npcname("#Bakonawan3-1")+"::OnEnd";
		killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
		stopnpctimer;
		disablenpc instance_npcname("#Bakonawan3");
	}
	end;
OnTimer1000:
	callsub OnAnnounce,"10 minutes",15,1;
	end;
OnTimer60000:
OnTimer120000:
OnTimer180000:
OnTimer240000:
	donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
OnTimer300000:	callsub OnAnnounce,"5 分鐘",15,1;
OnTimer360000:	callsub OnAnnounce,"4 分鐘",15,1;
OnTimer420000:	callsub OnAnnounce,"3 分鐘",15,1;
OnTimer480000:	callsub OnAnnounce,"2 分鐘",15,1;
OnTimer540000:	callsub OnAnnounce,"1 分鐘",15,1;
OnTimer570000:	callsub OnAnnounce,"30 秒",15,0;
OnTimer580000:	callsub OnAnnounce,"20 秒",15,0;
OnTimer590000:	callsub OnAnnounce,"10 秒",15,0;
OnTimer595000:	callsub OnAnnounce,"5 秒",16,0;
OnTimer596000:	callsub OnAnnounce,"4 秒",17,0;
OnTimer597000:	callsub OnAnnounce,"3 秒",18,0;
OnTimer598000:	callsub OnAnnounce,"2 秒",19,0;
OnTimer599000:	callsub OnAnnounce,"1 秒",20,0;
OnTimer600000:
	set .@map$, instance_mapname("1@ma_b");
	mapannounce .@map$,"暴龍逃進湖底了.",bc_map,"0xffff00"; //FW_NORMAL 20 0 0
	donpcevent instance_npcname("酸奶#Fail")+"::OnStart";
	killmonster .@map$,instance_npcname("#Bakonawan3")+"::OnMyMobDead";
	stopnpctimer;
	disablenpc instance_npcname("#Bakonawan3");
	end;
OnAnnounce:
	mapannounce instance_mapname("1@ma_b"),"Time Limit: "+getarg(0),bc_map,"0xff4400"; //FW_NORMAL getarg(1) 0 0
	if (getarg(2)) donpcevent instance_npcname("#SummonPuppetsn1")+"::OnStart";
	end;
}

1@ma_b,36,111,4	script	#Bakonawan3-1	844,{
	end;
OnInstanceInit:
	disablenpc instance_npcname("#Bakonawan3-1");
	end;
OnStart:
	initnpctimer;
	end;
OnEnd:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	//showdigit 0;
	stopnpctimer;
	end;
OnMyMobDead:
	showdigit mobcount(instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead");
	end;
OnTimer120000:	callsub OnMobSpawn,10;
OnTimer180000:	callsub OnMobSpawn,15;
OnTimer240000:	callsub OnMobSpawn,20;
//OnTimer300000:	callsub OnMobSpawn,25;
OnTimer300000:	callsub OnMobSpawn,30;
OnTimer360000:	callsub OnMobSpawn,35;
OnTimer420000:	callsub OnMobSpawn,40;
OnTimer480000:	callsub OnMobSpawn,45;
OnTimer540000:	callsub OnMobSpawn,50;
OnTimer600000:
	killmonster instance_mapname("1@ma_b"),instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	stopnpctimer;
	end;
OnMobSpawn:
	set .@label$, instance_npcname("#Bakonawan3-1")+"::OnMyMobDead";
	set .@map$, instance_mapname("1@ma_b");
	killmonster .@map$,.@label$;
	set .@mob_dead_num, mobcount(.@map$,.@label$);
	if (.@mob_dead_num < 50) {
		set .@mob_rg, 50 - .@mob_dead_num;
		if (.@mob_rg > getarg(0))
			set .@mob_rg, getarg(0);
		while(1) {
			areamonster .@map$,74,74,82,74,"食月暴龍的人偶",2334,1,.@label$;
			set .@mob_dead_num, mobcount(.@map$,.@label$);
			if (.@mob_dead_num >= .@mob_rg) {
				break;
			}
		}
	}
	end;
}

1@ma_b,62,52,4	script	酸奶#Completed	541,{
	disable_items;
	if (checkweight(1201,1) == 0) {
		mes "您攜帶的物品种類太多, 請稍做處理.";
		close;
	}
	if (MaxWeight - Weight < 10000) {
		mes "您的負重不足, 請稍做處理.";
		close;
              }
	if (checkquest(12279,HUNTING) == 2) {
		erasequest 12279;
		mes "[酸奶]";
		mes "這是從暴龍身上掉落的. 我想應該對你有用.";
		if (!VIP_SCRIPT || vip_status(1))
			getitem 6499,7; //Ancient_Grudge
		else
			getitem 6499,5; //Ancient_Grudge
		getexp 0,100000;
		next;
	}
	mes "[酸奶]";
	mes "我們最好快點离開這里, 就算暴龍不在這里還是讓人不舒服...";
	next;
	mes "[酸奶]";
	mes "讓我們用繩子上去!";
	close2;
	warp "ma_scene01",175,176;
	end;
OnInstanceInit:
	disablenpc instance_npcname("酸奶#Completed");
	end;
OnStart:
	enablenpc instance_npcname("酸奶#Completed");
	initnpctimer;
	end;
OnTimer1000:
	monster instance_mapname("1@ma_b"),78,74,"食月暴龍掉落的寶盒",2335,1;
	end;
OnTimer10000:
	mapannounce instance_mapname("1@ma_b"),"酸奶: 打開寶盒后, 請到山頂來找我. 我有些東西要給你.",bc_map,"0x00ffff"; //FW_NORMAL 15 0 0
	stopnpctimer;
	end;
}

1@ma_b,62,52,4	script	酸奶#Fail	541,{
	mes "[酸奶]";
	mes "嗯. 我們差點就抓住它了, 它動作非常敏捷...";
	next;
	if (is_party_leader() == true) {
		mes "[酸奶]";
		mes "想不想再來次?";
		next;
		switch(select("- 打住!:- 當然! 我們不能袖手旁觀!")) {
		case 1:
			mes "[酸奶]";
			mes "啊哈哈... 差點就逮住它了.";
			close;
		case 2:
			mes "[酸奶]";
			mes "我會在湖里再用次 Albopal.";
			next;
			mes "[酸奶]";
			mes "我想我們愛上暴龍狂怒時的那張臉了...";
			disablenpc instance_npcname("酸奶#Fail");
			donpcevent instance_npcname("#Bakonawan1")+"::OnStart";
			close;
		}
		close;
	} else {
		mes "[酸奶]";
		mes "我會像上次一樣向湖里洒 Albopal. 我期待看到它拉屎的樣子...";
		close;
	}

OnInstanceInit:
	disablenpc instance_npcname("酸奶#Fail");
	end;
OnStart:
	enablenpc instance_npcname("酸奶#Fail");
	end;
}
