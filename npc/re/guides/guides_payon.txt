//===== rAthena Script =======================================
//= Payon Guides
//===== Description: =========================================
//= Guides for the city of Payon.
//===== Changelogs: ==========================================
//= 1.0 Initial release for Renewal [L0ne_W0lf]
//= 1.1 Fixed and optimized. [Euphy]
//= 1.2 Navigation system update. [Euphy]
//============================================================

payon,163,67,3	script	解说员#01payon::GuidePayon	708,{
	cutin "pay_soldier",2;
	mes "[斐扬 解说员]";
	mes "这里是山岳之都 ^8B4513斐扬^000000，";
	mes "欢迎您的大驾光临，";
	mes "需要为您介绍吗?";
	F_Navi("[斐扬 解说员]");
	next;
	while (1) {
		switch(select("[ 主要设施 ]:[ 商人&小帮手 ]:删除小地图的标记:结束对话")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 弓箭手村 ]:[ 道具店 ]:[ 武器、防具店 ]:[ 旅馆及酒店 ]:[ 中央宫 ]:[ 内宫 ]:[ 宫廷御厨 ]:返回上一选单")) {
				case 1:
					mes "[斐扬 解说员]";
					mes "这里是弓箭手公会的所在地，";
					mes "可经由北侧底端的门";
					mes "前往"+F_Navi("弓箭手村","payon,227,328","^B9062F")+"，";
					mes "还需要其它服务吗?";
					viewpoint 1,227,328,0,0xFF0000;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("道具店","payon,144,85","^0000FF");
					viewpoint 1,144,85,1,0x0A82FF;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("武器、防具店","payon,139,159","^FF5400");
					viewpoint 1,139,159,2,0xFFB400;
					next;
					break;
				case 4:
					callsub L_Mark, F_Navi("旅馆及酒店","payon,220,117","^006400");
					viewpoint 1,220,117,3,0xAAFF00;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("中央宫","payon,155,245","^7F3300");
					viewpoint 1,155,245,4,0xD2691E;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("内宫","payon,107,325","^800080");
					viewpoint 1,107,325,5,0xDA70D6;
					next;
					break;
				case 7:
					callsub L_Mark, F_Navi("宫廷御厨","payon,154,325","^B9062F");
					viewpoint 1,154,325,6,0xFF0000;
					next;
					break;
				case 8:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 乐园团传送师 ]:[ 商城物品销售解说员 ]:[ 维修及精炼师 ]:[ 卡普拉职员 ]:[ 陷阱专家 ]:[ 世亚布连 ]:[ 雷亚布连 ]:[ 杰宜德 ]:[ 认养宠物商人 ]:[ 料理材料商人 ]:返回上一选单")) {
				case 1:
					callsub L_Mark, F_Navi("乐园团传送师","payon,177,111","^0000FF");
					viewpoint 1,177,111,7,0x0A82FF;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("商城物品销售解说员","payon,164,157","^FF5400");
					viewpoint 1,164,157,8,0xFFB400;
					next;
					break;
				case 3:
					callsub L_Mark, F_Navi("维修及精炼师","payon,144,170","^006400");
					viewpoint 1,144,170,9,0xAAFF00;
					next;
					break;
				case 4:
					callsub L_Mark, "^7F3300卡普拉职员s^000000";
					viewpoint 1,181,104,10,0xD2691E;
					viewpoint 1,175,226,11,0xD2691E;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("陷阱专家","payon,123,109","^800080");
					viewpoint 1,123,109,12,0xDA70D6;
					next;
					break;
				case 6:
					mes "[斐扬 解说员]";
					mes F_Navi("世亚布连","payon,140,151","^B9062F");
					mes "在武器或防具上提供^0000FF打洞^000000";
					mes "服务的专业技术员，";
					mes "只是并非所有的";
					mes "东西都可以哦!";
					mes "还需要其它服务吗?";
					viewpoint 1,140,151,13,0xFF0000;
					next;
					break;
				case 7:
					mes "[斐扬 解说员]";
					mes F_Navi("雷亚布连","payon,236,199","^0000FF");
					mes "在武器或防具上提供^0000FF打洞^000000";
					mes "服务的专业技术员，";
					mes "只是并非所有的";
					mes "东西都可以哦!";
					mes "还需要其它服务吗?";
					viewpoint 1,236,199,14,0x0A82FF;
					next;
					break;
				case 8:
					mes "[斐扬 解说员]";
					mes F_Navi("杰宜德","payon,173,238","^FF5400");
					mes "将各式各类的^FF5400魔力矿石^000000";
					mes "提供交换的服务，";
					mes "还需要其它服务吗?";
					viewpoint 1,173,238,15,0xFFB400;
					next;
					break;
				case 9:
					callsub L_Mark, F_Navi("认养宠物商人","payon,177,131","^006400");
					viewpoint 1,177,131,16,0xAAFF00;
					next;
					break;
				case 10:
					callsub L_Mark, F_Navi("料理材料商人","payon,206,119","^7F3300");
					viewpoint 1,206,119,17,0xD2691E;
					next;
					break;
				case 11:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[斐扬 解说员]";
			mes "已删除小地图上的标志了，";
			mes "还需要其它服务吗?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			viewpoint 2,1,1,12,0xFFFF00;
			viewpoint 2,1,1,13,0xFFFF00;
			viewpoint 2,1,1,14,0xFFFF00;
			viewpoint 2,1,1,15,0xFFFF00;
			viewpoint 2,1,1,16,0xFFFF00;
			viewpoint 2,1,1,17,0xFFFF00;
			next;
			break;
		case 4:
			mes "[斐扬 解说员]";
			mes "祝您旅途愉快!";
			close;
			cutin "",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[斐扬 解说员]";
	mes ""+getarg(0)+"的";
	mes "位置已经标记在";
	mes "小地图上了，";
	mes "还需要其它服务吗?";
	return;
}

payon,151,182,6	duplicate(GuidePayon)	解说员#02payon	708
payon,221,85,6	duplicate(GuidePayon)	解说员#03payon	708
payon,233,324,4	duplicate(GuidePayon)	解说员#04payon	708

pay_arche,86,33,4	script	解说员#05payon	708,{
	cutin "pay_soldier",2;
	mes "[斐扬 解说员]";
	mes "欢迎光临!";
	mes "这里是隶属於山岳之都斐扬的";
	mes "^8B4513弓箭手村^000000，";
	mes "需要为您介绍吗?";
	F_Navi("[斐扬 解说员]");
	next;
	set .@str$,"还需要其它服务吗?";
	while (1) {
		switch(select("[ 斐扬迷宫 ]:[ 弓箭手公会 ]:[ 道具店 ]:[ 卡普拉职员 ]:[ 佣兵公会管理员 ]:[ 链金术师公会会员 ]:[ 乐园团传送师 ]:删除小地图的标记:结束对话")) {
		case 1:
			mes "[斐扬 解说员]";
			mes F_Navi("斐扬迷宫入口","pay_arche,38,131","^B9062F")+"的";
			mes "位置已经标记在";
			mes "小地图上了，";
			mes .@str$;
			viewpoint 1,38,131,0,0xFF0000;
			next;
			break;
		case 2:
			mes "[斐扬 解说员]";
			mes F_Navi("弓箭手公会","pay_arche,143,164","^0000FF")+"是";
			mes "想要转职为弓箭手的";
			mes "冒险家们必来的";
			mes "地方，";
			mes .@str$;
			viewpoint 1,143,164,1,0x0A82FF;
			next;
			break;
		case 3:
			mes "[斐扬 解说员]";
			mes F_Navi("道具店","pay_arche,72,156","^FF5400");
			mes "位置已经标记在";
			mes "小地图上了，";
			mes .@str$;
			viewpoint 1,72,156,2,0xFFB400;
			next;
			break;
		case 4:
			mes "[斐扬 解说员]";
			mes "^006400卡普拉职员^000000的";
			mes "位置已经标记在";
			mes "小地图上了，";
			mes .@str$;
			viewpoint 1,55,123,3,0xAAFF00;
			next;
			break;
		case 5:
			mes "[斐扬 解说员]";
			mes F_Navi("佣兵公会管理员","pay_arche,101,166","^7F3300");
			mes "位置已经标记在";
			mes "小地图上了，";
			mes .@str$;
			viewpoint 1,101,166,4,0xD2691E;
			next;
			break;
		case 6:
			mes "[斐扬 解说员]";
			mes F_Navi("链金术师公会会员","pay_arche,126,113,","^800080");
			mes "位置已经标记在";
			mes "小地图上了，";
			mes .@str$;
			viewpoint 1,127,113,5,0xDA70D6;
			next;
			break;
		case 7:
			mes "[斐扬 解说员]";
			mes F_Navi("乐园团传送师","pay_arche,143,164","^0000FF")+"目前";
			mes "位於弓箭手公会的大楼前，";
			mes .@str$;
			viewpoint 1,143,164,6,0x0A82FF;
			next;
			break;
		case 8:
			mes "[斐扬 解说员]";
			mes "已删除小地图上的标志了，";
			mes .@str$;
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			next;
			break;
		case 9:
			mes "[斐扬 解说员]";
			mes "祝您旅途愉快!";
			close2;
			cutin "",255;
			end;
		}
	}
}
