//===== rAthena Script ======================================= 
//= Morocc Guides
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Guides for the city of Morocc and Morocc Ruins.
//===== Additional Comments: ================================= 
//= 1.0 First version, Renewal guide.
//= 1.1 Fixed and optimized. [Euphy]
//= 1.2 Navigation system update. [Euphy]
//============================================================ 

morocc,153,286,6	script	解說員#01morocc::GuideMorocc	707,{
	cutin "moc_soldier",2;
	mes "[夢羅克 解說員]";
	mes "這里是沙漠之都";
	mes "^8B4513夢羅克^000000，";
	mes "誠摯地幻影大家的蒞臨，";
	mes "需要為您介紹嗎?";
	F_Navi("[夢羅克 解說員]");
	next;
	while (1) {
		switch(select("[ 主要設施 ]:[ 商人&小幫手 ]:刪除小地圖的標記:結束對話")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 盜賊公會 ]:[ 史芬克斯密穴 ]:[ 綠洲入口 ]:[ 精煉工房 ]:[ 武器、防具店 ]:[ 旅館 ]:[ 酒店 ]:返回上一選單")) {
				case 1:
					mes "[夢羅克 解說員]";
					mes F_Navi("盜賊公會","morocc,26,294","^B9062F");
					mes "位于金字塔內，";
					mes "^B9062FPyramids^000000是在";
					mes "^B9062F綠洲^000000附近，";
					mes "還需要其它服務嗎?";
					viewpoint 1,26,294,0,0xFF0000;
					next;
					break;
				case 2:
					mes "[夢羅克 解說員]";
					mes F_Navi("史芬克斯密穴","morocc,26,164","^0000FF");
					mes "從夢羅克西門出去";
					mes "就能找到了，";
					mes "還需要其它服務嗎?";
					viewpoint 1,26,164,1,0x0A82FF;
					next;
					break;
				case 3:
					mes "[夢羅克 解說員]";
					mes "前往"+F_Navi("綠洲","morocc,26,294","^FF5400");
					mes "出入口的位置，";
					mes "已經標記在";
					mes "小地圖上了，";
					mes "還需要其它服務嗎?";
					viewpoint 1,26,294,2,0xFFB400;
					next;
					break;
				case 4:
					callsub L_Mark, F_Navi("精煉工房","morocc,47,47","^006400");
					viewpoint 1,47,47,3,0xAAFF00;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("武器、防具店","morocc,253,56","^7F3300");
					viewpoint 1,253,56,4,0xD2691E;
					next;
					break;
				case 6:
					mes "[夢羅克 解說員]";
					mes "目前暫停營業中的";
					mes F_Navi("旅館","morocc,273,269","^800080")+"的";
					mes "位置已經標記在";
					mes "小地圖上了，";
					mes "還需要其它服務嗎?";
					viewpoint 1,273,269,5,0xDA70D6;
					next;
					break;
				case 7:
					callsub L_Mark, F_Navi("酒店","morocc,52,259","^B9062F");
					viewpoint 1,52,259,6,0xFF0000;
					next;
					break;
				case 8:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 卡普拉職員 ]:[ 毒草銷售員 ]:[ 染料制作員 ]:[ 賽亞布仁 ]:返回上一選單")) {
				case 1:
					callsub L_Mark,"^0000FF卡普拉職員^000000";
					viewpoint 1,156,97,7,0x0A82FF;
					viewpoint 1,160,258,8,0x0A82FF;
					next;
					break;
				case 2:
					callsub L_Mark, F_Navi("毒草銷售員","morocc,191,94","^006400");
					viewpoint 1,190,96,9,0xAAFF00;
					next;
					break;
				case 3:
					mes "[夢羅克 解說員]";
					mes F_Navi("染料制作員","morocc,273,269","^7F3300");
					mes "當前住在";
					mes "旅館的1樓，";
					mes "位置已經標記在";
					mes "小地圖上了，";
					mes "還需要其它服務嗎?";
					viewpoint 1,273,269,10,0xD2691E;
					next;
					break;
				case 4:
					mes "[夢羅克 解說員]";
					mes F_Navi("賽亞布仁","morocc,51,41","^800080")+"是";
					mes "在武器或防具上提供 ^0000FF打洞^000000";
					mes "服務的專業技術員，";
					mes "只是并非所有的";
					mes "東西都可以喔!";
					mes "還需要其它服務嗎?";
					viewpoint 1,51,41,11,0xDA70D6;
					next;
					break;
				case 5:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[夢羅克 解說員]";
			mes "已刪除小地圖上的標志了，";
			mes "還需要其它服務嗎?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			next;
			break;
		case 4:
			mes "[夢羅克 解說員]";
			mes "祝你旅途平安!";
			close2;
			cutin "moc_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[夢羅克 解說員]";
	mes getarg(0)+"的位置";
	mes "已經標記在";
	mes "小地圖上了，";
	mes "還需要其它服務嗎?";
	return;
}
morocc,154,38,6	duplicate(GuideMorocc)	解說員#02morocc	707
morocc,296,213,4	duplicate(GuideMorocc)	解說員#03morocc	707
morocc,28,170,5	duplicate(GuideMorocc)	解說員#04morocc	707

moc_ruins,159,53,3	script	解說員#01ruins::GuideMocRuins	707,{
	cutin "moc_soldier",2;
	mes "[夢羅克 解說員]";
	mes "這里是沙漠之都";
	mes "^8B4513夢羅克^000000，";
	mes "誠摯地歡迎大家的蒞臨，";
	mes "需要為您介紹嗎?";
	F_Navi("[夢羅克 解說員]");
	next;
	while (1) {
		switch(select("[ 主要設施 ]:[ 商人&小幫手 ]:刪除小地圖的標記:結束對話")) {
		case 1:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 盜賊公會 ]:[ 史芬克斯密穴 ]:[ 夢羅克入口 ]:返回上一選單")) {
				case 1:
					mes "[夢羅克 解說員]";
					mes F_Navi("盜賊公會","moc_ruins,56,161","^B9062F");
					mes "位於金字塔內，";
					mes "^B9062F金字塔^000000的位置";
					mes "已經標記在";
					mes "小地圖上了，";
					mes "還需要其它服務嗎?";
					viewpoint 1,56,161,0,0xFF0000;
					next;
					break;
				case 2:
					mes "[夢羅克 解說員]";
					mes F_Navi("史芬克斯密穴","moc_ruins,71,19","^0000FF");
					mes "從夢羅克西門出去";
					mes "就能找到了，";
					mes "還需要其它服務嗎?";
					viewpoint 1,71,19,1,0x0A82FF;
					next;
					break;
				case 3:
					mes "[夢羅克 解說員]";
					mes "前往"+F_Navi("Morocc","moc_ruins,158,40","^FF5400");
					mes "東南方出口的位置";
					mes "已經標記在";
					mes "小地圖上了，";
					mes "還需要其它服務嗎?";
					viewpoint 1,158,40,2,0xFFB400;
					next;
					break;
				case 4:
					set .@loop,0;
					break;
				}
			}
			break;
		case 2:
			set .@loop,1;
			while (.@loop) {
				switch(select("[ 樂園團傳送師 ]:[ 道具商人 ]:[ 卡普拉職員 ]:[ 維修工人 ]:[ 認養寵物商人 ]:[ 料理材料商人 ]:[ 雷亞布連 ]:返回上一選單")) {
				case 1:
					callsub L_Mark, F_Navi("樂園團傳送師","moc_ruins,68,164","^006400");
					viewpoint 1,68,164,3,0xAAFF00;
					next;
					break;
				case 2:
					mes "[夢羅克 解說員]";
					mes F_Navi("北面道具商人","moc_ruins,92,128","^800080")+"与";
					mes F_Navi("南面道具商人","moc_ruins,114,63","^800080")+"的";
					mes "位置已經標記在";
					mes "小地圖上了，";
					mes "還需要其它服務嗎?";
					viewpoint 1,92,128,5,0xDA70D6;
					viewpoint 1,114,63,6,0xDA70D6;
					next;
					break;
				case 3:
					callsub L_Mark, "^B9062F卡普拉職員^000000";
					viewpoint 1,59,157,7,0xFF0000;
					next;
					break;
				case 4:
					callsub L_Mark, F_Navi("維修工人","moc_ruins,107,94","^0000FF");
					viewpoint 1,107,94,8,0x0A82FF;
					next;
					break;
				case 5:
					callsub L_Mark, F_Navi("認養寵物商人","moc_ruins,118,170","^FF5400");
					viewpoint 1,118,170,9,0xFFB400;
					next;
					break;
				case 6:
					callsub L_Mark, F_Navi("料理材料商人","moc_ruins,115,123","^006400");
					viewpoint 1,115,123,10,0xAAFF00;
					next;
					break;
				case 7:
					mes "[夢羅克 解說員]";
					mes F_Navi("雷亞布連","moc_ruins,154,86","^7F3300")+"是";
					mes "在武器或防具上提供 ^0000FF打洞^000000";
					mes "服務的專業技術員，";
					mes "只是并非所有的";
					mes "東西都可以喔!";
					mes "還需要其它服務嗎?";
					viewpoint 1,154,86,11,0xD2691E;
					next;
					break;
				case 8:
					set .@loop,0;
					break;
				}
			}
			break;
		case 3:
			mes "[夢羅克 解說員]";
			mes "已刪除小地圖上的標志了，";
			mes "還需要其它服務嗎?";
			viewpoint 2,1,1,0,0xFFFF00;
			viewpoint 2,1,1,1,0x000000;
			viewpoint 2,1,1,2,0xFF0000;
			viewpoint 2,1,1,3,0xFFFF00;
			viewpoint 2,1,1,4,0xFFFF00;
			viewpoint 2,1,1,5,0xFFFF00;
			viewpoint 2,1,1,6,0xFFFF00;
			viewpoint 2,1,1,7,0xFFFF00;
			viewpoint 2,1,1,8,0xFFFF00;
			viewpoint 2,1,1,9,0xFFFF00;
			viewpoint 2,1,1,10,0xFFFF00;
			viewpoint 2,1,1,11,0xFFFF00;
			next;
			break;
		case 4:
			mes "[夢羅克 解說員]";
			mes "祝你旅途平安!";
			close2;
			cutin "moc_soldier",255;
			end;
		}
	}
	end;
L_Mark:
	mes "[夢羅克 解說員]";
	mes getarg(0)+"的位置";
	mes "已經標記在";
	mes "小地圖上了，";
	mes "還需要其它服務嗎?";
	return;
}
moc_ruins,70,164,3	duplicate(GuideMocRuins)	解說員#02ruins	707
moc_ruins,65,44,5	duplicate(GuideMocRuins)	解說員#03ruins	707
