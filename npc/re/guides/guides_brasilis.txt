//===== rAthena Script ======================================= 
//= Brasilis Guide
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Guide for the city of Brasilis.
//===== Additional Comments: ================================= 
//= 1.0 First Version, Renewal guide.
//= 1.1 Added a missing close.
//= 1.2 Navigation system update. [Euphy]
//= 2016.03.18 汉化部分文字 [RACN汉化组.至尊宝]
//= 2016.03.21 汉化完全 [RACN汉化组.杨梅]
//============================================================ 

brasilis,219,97,3	script	Brasilis Guide	478,{
	mes "[Brasilis 解说员]";
	mes "Welcome to ^8B4513Brasilis^000000, a country as passionate as the sun.";
	mes "If you have any questions, please ask me.";
	F_Navi("[Brasilis Guide]");
	next;
	switch(select("Ask about locations:删除小地图的标记:结束对话")) {
	case 1:
		mes "[Brasilis 解说员]";
		mes "Where can I guide you?";
		next;
		switch(select("[ Hotel ]:[ Jungle Cable ]:[ Art Museum ]:[ Market ]:[ Verass Monument ]")) {
		case 1:
			mes "[Brasilis 解说员]";
			mes "The "+F_Navi("Brasilis Hotel","brasilis,274,151")+" is located just above, ^FF3355+^000000.";
			mes "还需要其它服务吗?";
			viewpoint 1,274,151,2,0xFF3355;
			close;
		case 2:
			mes "[Brasilis 解说员]";
			mes "Do you want to go through the rough jungle? You can take a ";
			mes F_Navi("Jungle Cable","brasilis,308,335")+" here ^CE6300+^000000.";
			mes "还需要其它服务吗?";
			viewpoint 1,308,335,3,0xCE6300;
			close;
		case 3:
			mes "[Brasilis 解说员]";
			mes "The pride of Brasilis, the world scale "+F_Navi("Art Museum","brasilis,137,167")+" is at ^A5BAAD+^000000.";
			mes "还需要其它服务吗?";
			viewpoint 1,137,167,4,0x00FF00;
			close;
		case 4:
			mes "[Brasilis 解说员]";
			mes "You can buy items for hunting at the "+F_Navi("Market","brasilis,254,248")+" here ^55FF33+^000000.";
			mes "还需要其它服务吗?";
			viewpoint 1,254,248,5,0x55FF33;
			close;
		case 5:
			mes "[Brasilis 解说员]";
			mes "The iconic monument of Brasilis, the "+F_Navi("Verass Monument","brasilis,195,222")+" stands at ^3355FF+^000000.";
			mes "还需要其它服务吗?";
			viewpoint 1,195,235,6,0x3355FF;
			close;
		}
	case 2:
		mes "[Brasilis 解说员]";
		mes "已删除小地图上的标志了，";
		mes "还需要其它服务吗?";
		viewpoint 0,274,151,2,0x00FF00;
		viewpoint 0,308,335,3,0x00FF00;
		viewpoint 0,137,167,4,0x00FF00;
		viewpoint 0,254,248,5,0x00FF00;
		viewpoint 0,195,235,6,0x00FF00;
		close;
	case 3:
		mes "[Brasilis 解说员]";
		mes "Wandering on your own is always the best way to explore. Anyway, take care.";
		close;
	}
}
