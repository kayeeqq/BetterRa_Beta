//===== rAthena Script ======================================= 
//= Brasilis Town
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.4
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= [Official Conversion]
//= Brasilis Town Script
//===== Additional Comments: ================================= 
//= 1.0 First version. Transportation and Basic NPCs.
//= 1.1 Fixed Zeny not being removed for payment. [Kisuka]
//= 1.2 Updated dialog, added Ice Cream Maker. [L0ne_W0lf]
//= 1.3 Moved Alberta NPC to pre-re/re paths. [Euphy]
//= 1.4 Added VIP features. [Euphy]
//============================================================ 

// Brasilis Transportation :: brasilis_trans
//============================================================
/* Pre-Renewal coordinates: alberta,247,115,3 */
alberta,246,82,3	script	布蘭妮號 船員#bra2	100,{
	mes "[布蘭妮號 船員]";
	mes "嘿, 你有听過一個叫巴西的地方嗎?";
	mes "那理有點像沙漠都市夢羅克般炎熱、多雨. 是個非常神秘的地方.";
	next;
	mes "[布蘭妮號 船員]";
	mes "我們最近發現了一條新的遠洋航線, 不容易去到那邊.";
	if (VIP_SCRIPT) {
		set .@cost, vip_status(VIP_STATUS_ACTIVE)?1000:10000;
	mes "往返需 10,000 ! , VIP的話只要1,000! 如何?"; //custom translation (VIP)
	} else {
		set .@cost,10000;
		mes "往返需 10,000 ! 如何?";
	}
	next;
	switch(select("去巴西!:我要留在這.")) {
	case 1:
		if (Zeny < .@cost) {
			mes "[布蘭妮號 船員]";
			mes "I said "+callfunc("F_InsertComma",.@cost)+" zeny.";
			close;
		}
		else {
			mes "[布蘭妮號 船員]";
			mes "酷喔~!! 出發~!";
			set Zeny, Zeny-.@cost;
			close2;
			warp "brasilis",314,60;
			end;
		}
	case 2:
		mes "[布蘭妮號 船員]";
		mes "如果你感到興趣, 告訴我帶你過去.";
		close;
	}
}

brasilis,316,57,3	script	布蘭妮號 船員#bra1	4W_SAILOR,{
	mes "[布蘭妮號 船員]";
	mes "我的船要駛回艾爾貝塔, 你想搭船嗎?";
	next;
	switch(select("搭船.:不搭船~.")) {
	case 1:
		mes "[布蘭妮號 船員]";
		mes "當然還是自己的家最好.";
		close2;
		if (checkre(0))
			warp "alberta",243,82;
		else
			warp "alberta",244,115;
		end;
	case 2:
		mes "[布蘭妮號 船員]";
		mes "好的, 隨便你. 當我們回來會在見到你.";
		close;
	}
}

// Generic Brasilis NPCs
//============================================================
brasilis,155,165,3	script	告示牌#bra1	4_BULLETIN_BOARD2,{
	mes ":: 美術博物館 ::";
	close;
}

brasilis,191,239,3	script	告示牌#bra2	4_BULLETIN_BOARD2,{
	mes ":: 韋拉斯紀念碑 ::";
	close;
}

brasilis,240,247,3	script	告示牌#bra3	4_BULLETIN_BOARD2,{
	mes ":: 市集 ::";
	mes " ";
	mes "- 藥水与武器 -";
	close;
}

brasilis,303,309,3	script	告示牌#bra4	4_BULLETIN_BOARD2,{
	mes ":: 叢林纜車 ::";
	mes "";
	mes "- 心臟弱的玩家禁入 -";
	close;
}

brasilis,278,137,3	script	告示牌#bra5	4_BULLETIN_BOARD2,{
	mes ":: 巴西　大飯店 ::";
	close;
}

brasilis,137,77,5	script	霜淇淋寶貝	4_M_03,{
	mes "[霜淇淋寶貝]";
	mes "來喲~來喲~";
	mes "冰淇淋是炎熱夏天最佳消暑良伴~";
	mes "只要 ^3355FF100 Zeny^000000~";
	mes "霜淇淋~";
	mes "霜淇淋來了!";
	next;
	switch(select("給我一個!:關于霜淇淋?:取消交易.")) {
	case 1:
		mes "[霜淇淋寶貝]";
		mes "清涼美味的霜淇淋由于太受歡迎, .";
		mes "所以每人只能限購 5個-";
		next;
		while(1) {
			input .@input; //,1,5;
			if (.@input == 0) {
				mes "[霜淇淋寶貝]";
				mes "不買?";
				mes "為了下一位排隊購買霜淇淋的人, 請讓位好嗎.";
				close;
			}
			else if ((.@input < 0) || (.@input > 5)) {
				mes "[霜淇淋寶貝]";
				mes "喔喔.";
				mes "你有點貪心喔.";
				mes "吃超過5個的話, 到了晚上你的腸胃就會跟你作對吧?.";
				next;
			}
			else
				break;
		}
		set .@icecream_hap,.@input*100;
		if (Zeny < .@icecream_hap) {
			mes "[霜淇淋寶貝]";
			mes "??您的金幣似乎不足?.";
			mes "每個是 ^3355FF100 Zeny^000000~ !";
			close;
		}
		if (!checkweight(536,.@input)) {
			mes "[霜淇淋寶貝]";
			mes "你攜帶太多物品.";
			mes "請減少一些攜帶品在購買.";
			close;
		}
		set Zeny, Zeny - .@icecream_hap;
		getitem 536,.@input; //Ice_Cream
		close;
	case 2:
		mes "[霜淇淋寶貝]";
		mes "'霜淇淋...";
		mes "等等,";
		mes "你不知道什么是霜淇淋?";
		mes "你該不會是...";
		mes "生活在天龍國吧?";
		next;
		mes "[霜淇淋寶貝]";
		mes "所謂'霜淇淋'就是...冷凍的冰品.";
		mes "因熱量高營養丰富, 适當的吃才有益健康.";
		close;
	case 3:
		mes "[霜淇淋寶貝]";
		mes "不要錯過品嘗美味的冰淇淋喔~!";
		close;
	}
}
