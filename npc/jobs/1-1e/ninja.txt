//===== rAthena Script ======================================= 
//= Ninja Job Quest
//===== By: ================================================== 
//= Legionaire, Kisuka
//===== Current Version: ===================================== 
//= 1.7
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= Ninja Job Change Quest
//===== Additional Comments: ================================= 
//= 1.1 Optimized, updated, checked [Lupus]
//= 1.2 Added Akagi [Playtester]
//= 1.3 Updated dialogs to official ones and optimized [SinSloth]
//= 1.4 Fixed a minor bug [SinSloth]
//= 1.5 Updated to match AEGIS script and Added Quest Log commands. [Kisuka]
//= 1.6 Fixed AEGIS amatsu warp typo. [Kisuka]
//= 1.7 Kagerou/Oboro support (Class -> BaseClass) [Euphy]
//============================================================ 

alberta,30,65,3	script	赤城	730,{
	if (Class == Job_Novice) {
		if (JobLevel == 10) {
			mes "[船長]";
			mes "嗯...";
			mes "你必須來一下,";
			mes "感覺到某人";
			mes "正在這里等著你.";
			mes "告訴我, 你找到";
			mes "耐心的路徑嗎?";
			next;
			if(select("否:是") == 1) {
				mes "[船長]";
				mes "我看到.";
				mes "每個人都有自我,";
				mes "我認為. 照顧";
				mes "好你自己.";
				close;
			}
			mes "[船長]";
			mes "非常好.";
			mes "那么, 讓我";
			mes "讓我幫你";
			mes "走上正确道路...";
			close2;
			switch(rand(3)) {
				case 1:
					warp "amatsu",170,229;
					end;
				case 2:
					warp "amatsu",216,188;
					end;
				default:
					warp "amatsu",178,176;
					end;
			}
		} else {
			mes "[船長]";
			mes "Hm? 我不能";
			mes "不服務";
			mes "你直到你成長得";
			mes "更強壯一點...";
			close;
		}
	} else {
		mes "[船長]";
		mes "Hmm...";
		mes "你和我...";
		mes "我們是水平相當的";
		mes "作戰能力方面.";
		mes "也許我們可以討論";
		mes "在一起的時候.";
		close;
	}
}

que_ng,30,65,3	script	克爾加#nq	730,{
	if (Upper == 2) {
		mes "[美洲獅]";
		mes "我... 我從來沒有";
		mes "看到過孩子";
		mes "向你一樣強壯!";
		mes "走開, 你";
		mes "大自然的怪物!";
		close;
	}
	if (Class == Job_Novice) {
		if (JobLevel < 10) {
			mes "[美洲獅]";
			mes "嗯? 你來這里";
			mes "學習如何成為一個忍者?";
			mes "你經驗不是很";
			mes "足夠啊, 所以回去";
			mes "直到你更熟悉如何";
			mes "和怪物戰斗.";
			close;
		}
		if (NINJ_Q == 0) {
			mes "["+strcharinfo(0)+"]";
			mes "不好意思.";
			mes "你好?";
			next;
			mes "[克爾加]";
			mes "...............................";
			mes "你是怎么做到的?";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "做什么?我沒有";
			mes "做任何事情, 我想我沒有...";
			next;
			mes "[克爾加]";
			mes "你怎么可以";
			mes "看到我? 我應該是";
			mes "肉眼看不見的.";
			mes "啊, 我現在知道了. 野貓 喬";
			mes "一定讓你來殺我! 我不會上你的當! 去死!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "等一下!我都不";
			mes "知道誰是野貓 喬!";
			mes "冷靜下來, 沒有";
			mes "必要使用暴力!";
			next;
			mes "[克爾加]";
			mes "什么...?";
			mes "你怎么躲開";
			mes "我所有的攻擊?";
			mes "你有一點天賦,";
			mes "我會教給你.";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "...............................";
			mes "我來到這里希望";
			mes "改變我的職業";
			mes "成為一個忍者.";
			next;
			mes "[克爾加]";
			mes "...哦. 就是這樣?";
			mes "Hmm,你有巨大的";
			mes "潛力, 但我幫不了";
			mes "你現在. 我有太多的";
			mes "敵人, 我不能";
			mes "放松警惕,哪怕一秒鐘.";
			next;
			mes "[克爾加]";
			mes "那野貓 喬 是";
			mes "冷血無情的...!";
			mes "他可以在任何時候出現!";
			mes "他會隨時做任何事情";
			mes "來擊敗他的敵人!";
			next;
			mes "[克爾加]";
			mes "等一下,等一下, 我只是";
			mes "想到一些事情.";
			mes "也許你可以幫助我.";
			mes "我說你做, 我會教";
			mes "你一些技能如果你";
			mes "真的想當一名忍者.";
			next;
			if(select("- 當然.:- 不,謝謝.") == 2) {
				mes "[克爾加]";
				mes "嗯? 好的, 非常好.";
				mes "我依然不明白";
				mes "為什么我們不能";
				mes "彼此幫助";
				mes "在這個小小的困境里...";
				close;
			}
			mes "[克爾加]";
			mes "很好! 現在, 我想";
			mes "問 野貓 喬 是否愿意";
			mes "同意暫時休戰.";
			mes "我知道我們兩個";
			mes "沒有武器, 所以我們";
			mes "應該先拿到好的武器.";
			next;
			mes "[克爾加]";
			mes "請把這封信,";
			mes "交給 Wildcat";
			mes "喬 在 艾音布羅克. 他是一個";
			mes "偽裝高手, 所以請細心";
			mes "用眼睛找出他. 啊, ";
			mes "在高處尋找他.";
			next;
			mes "[克爾加]";
			mes "是的, 野貓 喬";
			mes "總有一個東西";
			mes "藏在高處.";
			mes "無論如何, 你把信給他";
			mes "之后, 回來然后";
			mes "讓我知道他的答复.";
			set NINJ_Q,1;
			setquest 6015;
			close;
		}
		else if (NINJ_Q == 1) {
			mes "[克爾加]";
			mes "即使這個任務";
			mes "不是那么迫切,";
			mes "請盡快到達";
			mes "艾音布羅克 和完成";
			mes "我給野貓 喬的信.";
			close;
		}
		else if (NINJ_Q == 2) {
			mes "[克爾加]";
			mes "你有沒有交";
			mes "信給野貓 喬?";
			mes "我還需要知道他";
			mes "怎么回應我";
			mes "休戰的建議. 無論如何, 看吧";
			mes "如果你能針對這一點.";
			close;
		}
		else if (NINJ_Q == 3) {
			mes "[克爾加]";
			mes "啊, 你已經回來了.";
			mes "野貓 喬 ";
			mes "有連同他的答复";
			mes "把你送回來嗎? 很好, 很好,";
			mes "讓我讀一下.";
			next;
			mes "[克爾加]";
			mes "什么...?! 他怎么可以";
			mes "拒絕我的";
			mes "休戰建議?!這只能表示";
			mes "他在制造另一個";
			mes "苦無. Nuts! 我必須";
			mes "赶上他, 否則我就是一個死人!";
			next;
			mes "[克爾加]";
			mes "听著, 你必須";
			mes "再一次幫助我! 我需要你";
			mes "收集一些材料";
			mes "我可以制作我自己的苦無和 野貓 喬戰斗. 那么, 我將繼續";
			mes "幫助你變更職業成為忍者.";
			next;
			mes "[克爾加]";
			mes "這些你需要";
			mes "帶給我的";
			mes "^3355FF5 賽依伐^000000 和";
			mes "^3355FF1 強化武器金屬-級數一^000000.";
			mes "請盡快";
			mes "拿到這些材料!";
			next;
			mes "["+strcharinfo(0)+"]";
			mes "嗯? 真有趣,";
			mes "野貓 喬 也在";
			mes "讓我收集";
			mes "那些相同的材料.";
			next;
			mes "[克爾加]";
			mes "該死! 這意味著...";
			mes "你也在幫助 喬";
			mes "制作他的苦無! 不!";
			mes "我早就應該想到";
			mes "這一點的! 很好,現在已經";
			mes "太遲了. 只能盡快了!";
			set NINJ_Q,4;
			changequest 6017,6018;
			close;
		}
		else if (NINJ_Q == 4) {
			if (countitem(7053) < 5 || countitem(1010) < 1) {
				mes "[克爾加]";
				mes "盡快帶回來";
				mes "^3355FF5 賽依伐^000000 和";
				mes "^3355FF1 強化武器金屬-級數一^000000 給我,";
				mes "我才可以制造";
				mes "我自己的 苦無和";
				mes "野貓 喬對抗!";
				close;
			}
			if (SkillPoint != 0) {
				mes "[克爾加]";
				mes "哇,哇...";
				mes "你還有一些";
				mes "剩余的技能點.";
				mes "你最好在轉職前";
				mes "先把它們全部";
				mes "都使用, 好嗎?";
				close;
			}
			mes "[克爾加]";
			mes "啊, 你把所有我需要";
			mes "的東西都帶回來了.";
			mes "你比我預期的";
			mes "要早, 嗯? 非常好,";
			mes "正如所承諾的, 我會把你";
			mes "轉職成為忍者.";
			next;
			mes "[克爾加]";
			mes "讓我正式介紹";
			mes "我自己. 我是Touga忍者隊里的";
			mes "高級忍者, 然后";
			mes "我是負責尋找卡茲瑪";
			mes "的搜索隊.";
			next;
			mes "[克爾加]";
			mes "卡茲瑪是我村庄的";
			mes "一名小偷, 但他";
			mes "逃跑了.這導致了我們";
			mes "忍者隊伍";
			mes "有了沖突.現在事情";
			mes "很麻煩...";
			next;
			mes "[克爾加]";
			mes "我一開始并不接受";
			mes "像你這樣的忍者,因為";
			mes "現在情況很复雜.";
			mes "盡管如此, 你也証明了";
			mes "你是真正值得加入";
			mes "忍者隊伍的.";
			next;
			mes "[克爾加]";
			mes "根据他的來信, 盡管";
			mes "喬對你的評价很高. 但";
			mes "要記住, 作為一名忍者, 你的";
			mes "任務是你的最优先級的. 但不要讓任務目標";
			mes "取代了你的良心.";
			next;
			mes "[克爾加]";
			mes "''保密高于一切.''";
			mes "保証我們的秘密處于黑暗之中";
			mes "你可以在商人那里";
			mes "購買或者賣忍者的武器";
			mes "請記得";
			mes "保持警惕.";
			next;
			mes "[克爾加]";
			mes "今天, 你已經";
			mes "成為驕傲的忍者軍團中";
			mes "一名忍者. 變成";
			mes "像風一樣靈活, ";
			mes "像暗影一樣安靜.";
			callfunc "Job_Change",Job_Ninja;
			set NINJ_Q,5;
			delitem 7053,5; // 賽依伐
			delitem 1010,1; // 強化武器金屬-級數一
			getitem 13010,1;	// Asura
			completequest 6018;
			close;
		}
		else {
			mes "[克爾加]";
			mes "你怎么了?";
			mes "刻苦訓練:你希望";
			mes "能消失得";
			mes "無影無蹤. 如果你";
			mes "能做到, 你能得到";
			mes "其他忍者的尊重~";
			close;
		}
	} else {
		if (BaseClass == Job_Ninja) {
			mes "[克爾加]";
			mes "你怎么了?";
			mes "刻苦訓練: 你希望";
			mes "能消失得";
			mes "無影無蹤. 如果你";
			mes "能做到, 你能得到";
			mes "其他忍者的尊重~";
			close;
		} else {
			mes "[克爾加]";
			mes "什么...?";
			mes "你怎么能夠";
			mes "發現我隱藏";
			mes "在陰影中?!";
			mes "你肯定是";
			mes "非一般的冒險家, 嗯?";
			close;
		}
	}
}

einbroch,184,194,3	script	可疑男子#nq	881,{
	if (NINJ_Q == 1) {
		mes "[可以的人]";
		mes "我以前去過很多";
		mes "國家, 但我從來沒有";
		mes "去過一個像艾音布羅克塔這么高";
		mes "的建筑. 所有";
		mes "在我家鄉的建筑";
		mes "都比這里的要渺小...";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "哦, 你來自";
		mes "Amatsu? 我在尋找";
		mes "某個叫";
		mes "野貓 喬的人.";
		next;
		mes "[可疑的人]";
		mes "...不. 不, 我真的是從";
		mes "依斯魯得島來的, 我只是";
		mes "為了艾音布羅克的一些礦物質";
		mes "而來. 告訴我, 你為什么";
		mes "尋找這個叫野貓 喬的人?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "好吧, 我需要把";
		mes "這封信送個他然后";
		mes "得到他的答复,那樣";
		mes "我才能成為一個忍者.";
		next;
		mes "[可疑的人]";
		mes "真的?那現在我";
		mes "想一下,我想我曾經遇到";
		mes "他一次或者兩次";
		mes "在這個鎮上. 所以, 他更可能被叫做''紅豹 喬,''";
		mes "而不是 ''野貓 喬.''";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我真的很希望能幫助你";
		mes "找到他, 但首先我必須";
		mes "找到我想找到的";
		mes "礦物質. 如果你不";
		mes "介意, 你能幫助我嗎?";
		mes "那么我可以幫助你找到喬.";
		next;
		if(select("- 沒關系, 我自己一個能找到他.:- 當然, 我會幫你.") == 1) {
			mes "[可疑的人]";
			mes "你能肯定...?";
			mes "紅豹 喬是一個真正的";
			mes "偽裝高手. 你會";
			mes "需要很多幫助,你才能";
			mes "找到他...";
			close;
		}
		mes "[可疑的人]";
		mes "很好,我很高興";
		mes "听到這個. 請";
		mes "幫我找到";
		mes "^3355FF5 賽依伐^000000 和";
		mes "^3355FF1 強化武器金屬-級數一^000000.";
		set NINJ_Q,2;
		changequest 6015,6016;
		close;
	}
	else if (NINJ_Q == 2) {
		if (countitem(7053) < 5 || countitem(1010) < 1) {
			mes "[可疑的人]";
			mes "請把";
			mes "^3355FF5 賽依伐^000000 和";
			mes "^3355FF1 強化武器金屬-級數一^000000 帶給我";
			mes "用你最快的速度. 那么,";
			mes "我能幫助你找到";
			mes "Red Leopard 喬.";
			close;
		}
		mes "[可疑的人]";
		mes "好, 好. 你已經";
		mes "帶來了礦物質...";
		mes "現在, 輪到我";
		mes "幫助你了. 來這里,";
		mes "讓我看一下那封信.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "?????!!";
		next;
		mes "[可疑的人]";
		mes "為什么? 你怎么不帶克爾加的信給我?";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "你是...";
		mes "你是野貓 喬?";
		next;
		mes "[可疑的人]";
		mes "...沒錯,但我比較喜歡";
		mes "被叫做紅豹 喬.";
		mes "克爾加把你送給我, 對吧?";
		mes "他是唯一一個這樣";
		mes "叫我的人. 你想成為";
		mes "一個忍者, 嗯? 哈,好吧.";
		next;
		mes "[紅豹 喬]";
		mes "如果你想成為一個忍者,";
		mes "你必須一直注意";
		mes "自己所看到的和相信的.不要忘記";
		mes "保守秘密,那么你就是一名合格的忍者.";
		next;
		mes "[紅豹 喬]";
		mes "記得行動要";
		mes "迅速, 和總要";
		mes "消失得無影無蹤.";
		mes "繼續隱藏在";
		mes "陰影中是我們真正的";
		mes "終极力量.";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "我知道了...";
		mes "...........";
		next;
		mes "[紅豹 喬]";
		mes "那么現在, 讓我看一下";
		mes "這封信. 我看一下...";
		mes "哈. 我想克爾加";
		mes "想再一次挑戰我";
		mes "但他現在真的想要";
		mes "一個停戰協議? 哈!";
		next;
		mes "[紅豹 喬]";
		mes "謝謝你的幫助,";
		mes "我現在有礦物質了";
		mes "我需要制造一個苦無!";
		mes "哈哈哈! 當我有优勢的時候我是不會同意停戰的!";
		next;
		mes "[紅豹 喬]";
		mes "無論怎樣, 讓我寫一個";
		mes "答复給他. 我也會";
		mes "給你我的建議...";
		mes "我想你會成為一個非常出色的";
		mes "忍者, 盡管我之前騙了你";
		mes "哈哈哈!";
		next;
		mes "["+strcharinfo(0)+"]";
		mes "......";
		mes ".........";
		mes "............";
		next;
		mes "[紅豹 喬]";
		mes "就這樣吧.";
		mes "請把這封信";
		mes "帶給克爾加.";
		mes "我需要一段時間";
		mes "才回去Amatsu, 所以讓";
		mes "直接幫你送到那里...";
		delitem 1010,1;	// 強化武器金屬-級數一
		delitem 7053,5;	// 賽依伐
		set NINJ_Q,3;
		changequest 6016,6017;
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if (NINJ_Q == 3) {
		mes "[紅豹 喬]";
		mes "額? 我不知道";
		mes "發生什么了, 但好像";
		mes "你沒有把我的答复";
		mes "交給克爾加.";
		mes "要我現在直接送你";
		mes "去Amatsu嗎?";
		next;
		if(select("不用, 謝謝.:好的, 麻煩了.") == 1) {
			mes "[紅豹 喬]";
			mes "很好. 好的, 我只是";
			mes "想幫你節約";
			mes "一點時間.";
			close;
		}
		mes "[紅豹 喬]";
		mes "好的, 那么.";
		mes "再見了.";
		close2;
		warp "amatsu",113,127;
		end;
	}
	else if (NINJ_Q == 4) {
		mes "[紅豹 喬]";
		mes "克爾加也要求你";
		mes "幫他收集一些材料?";
		mes "哦,好吧, 我想";
		mes "我不能怪他.";
		mes "此外, 我應該可以";
		mes "在公平的決斗中打敗他~";
		close;
	}
	else if (NINJ_Q == 5 && BaseClass == Job_Ninja) {
		mes "[紅豹 喬]";
		mes "啊, 你已經是一名忍者了~";
		mes "我希望你繼續";
		mes "訓練自己和盡力";
		mes "掌握所有的忍者技能";
		mes "永遠記得";
		mes "要隱藏在陰影之中.";
		close;
	}
	else {
		mes "[游客]";
		mes "我曾經去過很多";
		mes "國家, 但我從來沒有";
		mes "去過一個像艾音布羅克塔這么高";
		mes "的建筑.所有";
		mes "在我家鄉的建筑";
		mes "都比這里的要渺小...";
		close;
	}
}
