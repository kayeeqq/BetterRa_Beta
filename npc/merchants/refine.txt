//===== rAthena Script ======================================= 
//= Refining NPCs
//===== By: ==================================================
//= Syrus22 (1.1) dafide18 (1.4) Skotlex (1.5)
//===== Current Version: =====================================
//= 3.4
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Refining NPCs and Metal Salesmen.
//===== Additional Comments: =================================
//= 3.0 Updated several NPC names and locations. [Xantara]
//=     Added WoE map Refiners.
//= 3.1 Added the new refinement & Ore creation NPC's for +11 and above Refinement. [Masao]
//= 3.2 Moved some scripts to Renewal file, other minor changes. [Euphy]
//= 3.2a Added 'disable_items' command. [Euphy]
//= 3.3 Some official script updates. [Euphy]
//= 3.4 Added VIP features. [Euphy]
//============================================================

// Christopher: Geffen Blacksmith
//============================================================
geffen_in,110,172,0	script	克理斯多福#1	63,{
	mes "[克理斯多福]";
	mes "你好, 冒险家!";
	mes "欢迎来到克里斯多福铁匠铺.";
	next;
	mes "[克理斯多福]";
	mes "这里贩卖所有关于精炼用的物品.";
	mes "您需要什么呢?";
	next;
	switch(select("我想要买铁砧:我想要买精炼的物品:我想要买金属:我想要买制作原石:取消")) {
	case 1:
		mes "[克理斯多福]";
		mes "用来制作武器的铁砧,";
		mes "当然是越坚硬的金属越好.";
		mes "不过您应该也知道,";
		mes "愈坚硬的金属价格上就愈贵.";
		mes "您可以根据自己的经济情况选择合适的.";
		next;
		switch(select("铁砧 - 30,000z:神之金属铁砧 - 120,000z:黄金铁砧 - 300,000z:更好的铁砧:取消")) {
		case 1:
			if (Zeny < 30000) {
				mes "[克理斯多福]";
				mes "我觉得这个价格已经是最低的了，因为这么低的价钱，所以也不允许赊帐";
				close;
			}
			getitem 986,1; // Anvil
			Zeny = Zeny-30000;
			mes "[克理斯多福]";
			mes "你选择的这一种是比较便宜的一种.";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[克理斯多福]";
				mes "我觉得这个价格已经是最低的了，因为这么低的价钱，所以也不允许赊帐";
				close;
			}
			getitem 987,1; // Oridecon_Anvil
			Zeny = Zeny-120000;
			mes "[克理斯多福]";
			mes "噢!看来你是个行家。这一定是一个适合铁匠的铁砧，你说是吗?";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[克理斯多福]";
				mes "我觉得这个价格已经是最低的了，因为这么低的价钱，所以也不允许赊帐";
				close;
			}
			getitem 988,1; // Golden_Anvil
			Zeny = Zeny-300000;
			mes "[克理斯多福]";
			mes "这一个是工作室之中的最好的一种，用这种铁砧，你绝对可以称霸铁匠界";
			close;
		case 4:
			mes "[克理斯多福]";
			mes "很抱歉!";
			mes "我这里没有比黄金铁砧更好的铁砧了.";
			next;
			mes "[克理斯多福]";
			mes "或许传说中的'Ringgel'有比这更好的铁砧, 但是它只是一个传说而已.";
			close;
		case 5:
			mes "[克理斯多福]";
			mes "如果有任何需要的话, ";
			mes "欢迎您再度光临.";
			mes "请慢走~";
			close;
		}
	case 2:
		mes "[克理斯多福]";
		mes "伟大的铁匠当然得要用伟大的道具。";
		mes "如果在克里斯多福铁匠铺购买道具的话，";
		mes "您一定可以成为一名伟大的铁匠的。";
		mes "选选看那一种道具最何您的心意吧!";
		next;
		switch(select("携带用熔矿炉 - 150z:钢铁锤子 - 1000z:黄金锤子 - 3000z:神之金属锤子 - 5000z:结束交易")) {
		case 1:
			mes "[克理斯多福]";
			mes "好的，请问您要购买多少数量?";
			mes "欲结束交易的话请按'0'。";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[克理斯多福]";
					mes "交易已经结束。请慢走。";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[克理斯多福]";
					mes "请将购买数量减到500个以下。";
					next;
				}
				else {
					break;
				}
			}
			.@sell = .@input * 150;
			if (Zeny < .@sell) {
				mes "[克理斯多福]";
				mes "我觉得这个价格已经是最低的了，因为这么低的价钱，所以也不允许赊帐……";
				close;
			}
			if (checkweight(612,.@input) == 0) {
				mes "[克理斯多福]";
				mes "你没办法携带这么多东西唷!";
				close;
			}
			getitem 612,.@input; // Portable_Furnace
			Zeny = Zeny-.@sell;
			mes "[克理斯多福]";
			mes "感谢您的光临，欢迎下次光临!";
			close;
		case 2:
			if (Zeny < 1000) {
				mes "[克理斯多福]";
				mes "我觉得这个价格已经是最低的了，因为这么低的价钱，所以也不允许赊帐……";
				close;
			}
			getitem 613,1; // Iron_Hammer
			Zeny = Zeny-1000;
			mes "[克理斯多福]";
			mes "感谢您的光临，欢迎下次光临!";
			close;
		case 3:
			if (Zeny < 3000) {
				mes "[克理斯多福]";
				mes "我觉得这个价格已经是最低的了，因为这么低的价钱，所以也不允许赊帐……";
				close;
			}
			getitem 614,1; // Golden_Hammer
			Zeny = Zeny-3000;
			mes "[克理斯多福]";
			mes "感谢您的光临，欢迎下次光临!";
			close;
		case 4:
			if (Zeny < 5000) {
				mes "[克理斯多福]";
				mes "我觉得这个价格已经是最低的了，因为这么低的价钱，所以也不允许赊帐……";
				close;
			}
			getitem 615,1; // Oridecon_Hammer
			Zeny = Zeny-5000;
			mes "[克理斯多福]";
			mes "感谢您的光临，欢迎下次光临!";
			close;
		case 5:
			mes "[克理斯多福]";
			mes "欢迎来到'克理斯多福'的工作室";
			close;
		}
	case 3:
		mes "[克理斯多福]";
		mes "我准备了很多高质量的金属。现在你需要哪一种呢?";
		next;
		switch(select("强化武器金属-级数一 - 200z:强化武器金属-级数二 - 1000z:取消")) {
		case 1:
			mes "[克理斯多福]";
			mes "现在你需要买多少呢?如果你不想买任何东西，请输入数字'0'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[克理斯多福]";
					mes "交易取消";
					mes "再见!";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[克理斯多福]";
					mes "只能购买500个或者更少";
					next;
				}
				else {
					break;
				}
			}
			.@sell = .@input * 200;
			if (Zeny < .@sell) {
				mes "[克理斯多福]";
				mes "我觉得这个价格已经是最低的了，因为这么低的价钱，所以也不允许赊帐……";
				close;
			}
			if (checkweight(1010,.@input) == 0) {
				mes "[克理斯多福]";
				mes "你没办法携带这么多东西唷!";
				close;
			}
			getitem 1010,.@input; // Phracon
			Zeny = Zeny-.@sell;
			mes "[克理斯多福]";
			mes "感谢您的光临，欢迎下次光临!";
			close;
		case 2:
			mes "[克理斯多福]";
			mes "现在你需要买多少呢?如果你不想买任何东西，请输入数字'0'";
			next;
			while(1) {
				input .@input;
				if (.@input == 0) {
					mes "[克理斯多福]";
					mes "交易取消";
					mes "再见!";
					close;
				}
				else if ((.@input < 0) || (.@input > 500)) {
					mes "[克理斯多福]";
					mes "只能购买500个或者更少";
					next;
				}
				else {
					break;
				}
			}
			.@sell = .@input * 1000;
			if (Zeny < .@sell) {
				mes "[克理斯多福]";
				mes "我觉得这个价格已经是最低的了，因为这么低的价钱，所以也不允许赊帐……";
				close;
			}
			if (checkweight(1011,.@input) == 0) {
				mes "[克理斯多福]";
				mes "你没办法携带这么多东西唷!";
				close;
			}
			getitem 1011,.@input; // Emveretarcon
			Zeny = Zeny-.@sell;
			mes "[克理斯多福]";
			mes "感谢您的光临，欢迎下次光临!";
			close;
		case 3:
			mes "[克理斯多福]";
			mes "欢迎来到'克理斯多福'的工作室";
			close;
		}
	case 4:
		mes "[克理斯多福]";
		mes "我这里可以制作优质的金属矿石，5个神之金属原石可以制作成一个神之金属，5个铝原石可以制作成一个铝，你打算做哪一种呢?";
		next;
		switch(select("制作神之金属:制作铝:取消")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[克理斯多福]";
				mes "我告诉过你5个神之金属原石才能做一个神之金属";
				close;
			}
			else {
				delitem 756,5;  //Oridecon_Stone
				getitem 984,1; // Oridecon
				mes "[克理斯多福]";
				mes "给你一个神之金属，欢迎你下次再来!";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[克理斯多福]";
				mes "我告诉过你5个铝原石才能换一个铝";
				close;
			}
			else {
				delitem 757,5;  //Elunium_Stone
				getitem 985,1; // Elunium
				mes "[克理斯多福]";
				mes "给你一个铝，欢迎你下次再来!";
				close;
			}
		case 3:
			mes "[克理斯多福]";
			mes "你需要的时候，欢迎随时再来，再见!";
			close;
		}
	case 5:
		mes "[克理斯多福]";
		mes "欢迎随时来'克理斯多福'的工作室.";
		close;
	}
}

// Paul Spanner: Einbroch Blacksmith Supplier
//============================================================
ein_in01,38,29,0	script	Paul Spanner	63,{
	if (checkweight(1201,1) == 0) {
		mes "- Wait a minute !! -";
		mes "- Currently you're carrying -";
		mes "- too many items with you. -";
		mes "- Please try again -";
		mes "- after you lose some weight. -";
		close;
	}
	mes "[Paul Spanner]";
	mes "Welcome, my friend.";
	mes "In my shop, you will find everything that you need in forging.";
	mes "Tell me what you need.";
	next;
	switch(select("Purchase Anvil.:Purchase Forging Items.:Purchase Metal.:Process Ores.:Quit.")) {
	case 1:
		mes "[Paul Spanner]";
		mes "Anvil is the most necessary item for Blacksmiths.";
		mes "Since you will use an Anvil more than once, you'd better buy a nice one.";
		next;
		switch(select("Anvil - 30,000z.:Oridecon Anvil - 120,000z.:Golden Anvil - 300,000z.:I need a better anvil.:Cancel.")) {
		case 1:
			if (Zeny < 30000) {
				mes "[Paul Spanner]";
				mes "With that much of money, you cannot even buy a toy anvil!";
				close;
			}
			getitem 986,1; //Anvil
			Zeny = Zeny-30000;
			mes "[Paul Spanner]";
			mes "It is the cheapest anvil which has the most basic ability.";
			mes "Thank you for using my shop. If you need anything, just let me know.";
			close;
		case 2:
			if (Zeny < 120000) {
				mes "[Paul Spanner]";
				mes "With that much of money, you cannot even buy a toy anvil!";
				close;
			}
			getitem 987,1; //Oridecon_Anvil
			Zeny = Zeny-120000;
			mes "[Paul Spanner]";
			mes "Ah, you have an eye for anvil. A Blacksmith needs an anvil at least as good as this.";
			mes "Thank you for using my shop. If you need anything, just let me know.";
			close;
		case 3:
			if (Zeny < 300000) {
				mes "[Paul Spanner]";
				mes "With that much of money, you cannot even buy a toy anvil!";
				close;
			}
			getitem 988,1; //Golden_Anvil
			Zeny = Zeny-300000;
			mes "[Paul Spanner]";
			mes "I can tell your ambition to become a good Blacksmith just by looking at you to choose this Golden Anvil!";
			mes "This anvil will surely aid you in creating the best weapons.";
			close;
		case 4:
			mes "[Paul Spanner]";
			mes "I am sorry, but I do not sell better anvils than Golden Anvil.";
			mes "Unless you find the legendary anvil of 'Linggell', I don't think that you could find better one than Golden Anvil in any other places.";
			close;
		case 5:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
	case 2:
		mes "[Paul Spanner]";
		mes "You need various materials to process ores and to forge weapons.";
		mes "I have everything that you need. Take a look.";
		next;
		switch(select("Mini Furnace - 150z.:Iron Hammer - 1,000z.:Golden Hammer - 3,000z.:Oridecon Hammer - 5,000z.:Cancel.")) {
		case 1:
			.@item = 612;
			.@item_cost = 150;
			.@item_weight = 200;
			mes "[Paul Spanner]";
			mes "You definately need this furnce to process ores!";
			next;
			break;
		case 2:
			.@item = 613;
			.@item_cost = 1000;
			.@item_weight = 200;
			break;
		case 3:
			.@item = 614;
			.@item_cost = 3000;
			.@item_weight = 300;
			break;
		case 4:
			.@item = 615;
			.@item_cost = 5000;
			.@item_weight = 400;
			break;
		case 5:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
		mes "[Paul Spanner]";
		mes "So, how many do you need? If you want to cancel the trade, enter '0'.";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Paul Spanner]";
				mes "You have canceled the trade. If you need anything, just let me know.";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[Paul Spanner]";
				mes "You can only buy 500 or less at a time.";
				next;
			}
			else {
				break;
			}
		}
		.@sell = .@input * .@item_cost;
		if (Zeny < .@sell) {
			mes "[Paul Spanner]";
			mes "You don't have enough money. Sorry, I cannot sell them at a loss.";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[Paul Spanner]";
			mes "Hey, you look pale. Why don't you go lighten your weight first.";
			close;
		}
		Zeny = Zeny-.@sell;
		getitem .@item,.@input;
		mes "[Paul Spanner]";
		mes "Thank you for using my shop. If you need anything, just let me know.";
		close;
	case 3:
		mes "[Paul Spanner]";
		mes "I have high quality metal.";
		mes "So, which metal would you like to buy?";
		next;
		switch(select("Phracon - 200z.:Emveretarcon - 1,000z.:Quit.")) {
		case 1:
			.@item = 1010;
			.@item_price = 200;
			break;
		case 2:
			.@item = 1011;
			.@item_price = 1000;
			break;
		case 3:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
		mes "[Paul Spanner]";
		mes "So, how many of them do you need? If you want to cancel the trade, enter '0'.";
		next;
		while(1) {
			input .@input;
			if (.@input == 0) {
				mes "[Paul Spanner]";
				mes "The trade has been canceled. If you need anything, just let me know.";
				close;
			}
			else if ((.@input < 0) || (.@input > 500)) {
				mes "[Paul Spanner]";
				mes "You can buy 500 or less at a time.";
				next;
			}
			else {
				break;
			}
		}
		.@sell = .@input * .@item_price;
		if (Zeny < .@sell) {
			mes "[Paul Spanner]";
			mes "You don't have enough money. Sorry, I cannot sell them at a loss.";
			close;
		}
		if (checkweight(.@item,.@input) == 0) {
			mes "[Paul Spanner]";
			mes "Hey, you look pale. Why don't you go lighten your weight first?";
			close;
		}
		getitem .@item,.@input;
		Zeny = Zeny-.@sell;
		mes "[Paul Spanner]";
		mes "Thank you for using my shop. If you need anything, just let me know.";
		close;
	case 4:
		mes "[Paul Spanner]";
		mes "I can process Oridecon and Elunium for you.";
		mes "You need 5 ores to process them into one Oridecon or Elunium.";
		mes "So, which one do you want to process?";
		switch(select("Oridecon:Elunium:Quit.")) {
		case 1:
			if (countitem(756) < 5) {
				mes "[Paul Spanner]";
				mes "You need 5 ores to process them into one pure Oridecon.";
				close;
			}
			else {
				delitem 756,5; //Oridecon_Stone
				getitem 984,1; //Oridecon
				mes "[Paul Spanner]";
				mes "There you go. Thank you for using my service.";
				close;
			}
		case 2:
			if (countitem(757) < 5) {
				mes "[Paul Spanner]";
				mes "You need 5 ores to process them into one pure Elunium.";
				close;
			}
			else {
				delitem 757,5; //Elunium_Stone
				getitem 985,1; //Elunium
				mes "[Paul Spanner]";
				mes "There you go. Thank you for using my service.";
				close;
			}
		case 3:
			mes "[Paul Spanner]";
			mes "If you need anything, just let me know.";
			close;
		}
	case 5:
		mes "[Paul Spanner]";
		mes "If you need anything, just let me know.";
		close;
	}
}

// Weapon/Armor Refiners
//============================================================
prt_in,63,60,0	script	忽克连	85,{
	callfunc "refinemain","忽克连",0;
	end;
}
morocc_in,73,38,6	script	亚拉甘	99,{
	callfunc "refinemain","亚拉甘",0;
	end;
}
payon,144,173,5	script	安东尼奥	88,{
	callfunc "refinemain","安东尼奥",0;
	end;
}
alberta_in,28,58,0	script	弗雷德利	85,{
	callfunc "refinemain","弗雷德利",0;
	end;
}
yuno_in01,171,21,4	script	尤思特	88,{
	callfunc "refinemain","尤思特",0;
	end;
}
ein_in01,24,87,5	script	曼塔斯曼	826,{
	callfunc "refinemain","曼塔斯曼",0;
	end;
}
lhz_in02,282,20,7	script	福乐	869,{
	callfunc "refinemain","福乐",0;
	end;
}

//============================================================
//= Main Refiner Function
//============================================================
//= To allow auto safe refining/multiple refining set the
//= second argument to '1' in the function call.
//= If you enable this function, be sure to edit the value of
//= .@safe to the max safe refine in refine_db.txt as well.
//============================================================
function	script	refinemain	{
	.@npc_name$ = getarg(0);
	.@features = getarg(1);

	if( getbattleflag( "feature.refineui" ) == 3 ){
		mes "["+ .@npc_name$ +"]";
		mes "I've spent a long time refining powerful weapons.";
		mes "You are in the presence of ???? [Zhang Zhuangtan]"; // TODO: find a valid translation
		mes "Do you want to strengthen your equipment?";
		close2;
		refineui();
		end;
	}

	disable_items;
	mes "["+ .@npc_name$ +"]";
	mes "I'm the Armsmith.";
	mes "I can refine all kinds of weapons, armor and equipment, so let me";
	mes "know what you want me to refine.";
	next;

	setarray .@indices[1], EQI_HEAD_TOP, EQI_ARMOR, EQI_HAND_L, EQI_HAND_R, EQI_GARMENT, EQI_SHOES, EQI_ACC_L, EQI_ACC_R, EQI_HEAD_MID, EQI_HEAD_LOW;
	for(.@i = 1; .@i<getarraysize(.@indices); ++.@i) {
		if(getequipisequiped(.@indices[.@i])) {
			.@menu$ = .@menu$ + F_getpositionname(.@indices[.@i]) + "-[" + getequipname(.@indices[.@i]) + "]";
			.@equipped = 1;
		}
		else
			set .@menu$, .@menu$ + F_getpositionname(.@indices[.@i]) + "-[没有装备]";
		.@menu$ = .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "[" + .@npc_name$ + "]";
		mes "你好歹倒是把装备穿上先...";
		close;
	}
	.@part = .@indices[select(.@menu$)];

	if(!getequipisequiped(.@part)) { //custom check
		mes "[" + .@npc_name$ + "]";
		mes "要不要我把你的头脑精炼得";
		mes "聪明一点啊?";
		emotion ET_FRET;
		close;
	}
	//Check if the item is refinable...
	if(!getequipisenableref(.@part)) {
		mes "[" + .@npc_name$ + "]";
		mes "你选中的这个装备是无法精炼的道具喔!";
		close;
	}
	//Check to see if the items is already +10
	if(getequiprefinerycnt(.@part) >= 10) {
		mes "[" + .@npc_name$ + "]";
		mes "这件装备已经锻治到10级了.";
		mes "以我的能力, 无法再锻造了..";
		close;
	}
	.@refineitemid = getequipid(.@part); // save id of the item
	.@refinerycnt = getequiprefinerycnt(.@part); //save refinery count
	setarray .@card[0], getequipcardid(.@part,0), getequipcardid(.@part,1), getequipcardid(.@part,2), getequipcardid(.@part,3);
	.@price = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_ZENY_COST);
	.@material = getequiprefinecost(.@part, REFINE_COST_NORMAL, REFINE_MATERIAL_ID);

	switch (getequipweaponlv(.@part)) {
		case 1: .@safe = 7; break;
		case 2: .@safe = 6; break;
		case 3: .@safe = 5; break;
		case 4:
		default: .@safe = 4; break;
	}

	// If the VIP system is enabled, the prices for non-VIP players are considerably higher.
	if (VIP_SCRIPT && !vip_status(VIP_STATUS_ACTIVE)) {
		switch(getequipweaponlv(.@part)) {
			case 0: .@price = .@price * 10; break;
			case 1: .@price = .@price * 40; break;
			case 2: .@price = .@price * 50; break;
			case 3: .@price = .@price * 2; break;
			case 4: .@price = .@price * 2; break;
			case 5: .@price = .@price * 10; break;
		}
	}

	if(.@features != 1) {
		mes "[" + .@npc_name$ + "]";
		mes "精练这个物品我需要:";
		mes "- ^0000ff"+getitemname(.@material)+"^000000 x 1";
		mes "- 精炼服务费 ^ff0000"+ .@price + "^000000 Zeny";
		mes "你真的希望我继续吗?";
		next;
		if(select("是的, 继续吧:还是算了吧...") == 2){
			mes "[" + .@npc_name$ + "]";
			mes "耶...";
			mes "那好吧, 欢迎下次再来!";
			close;
		}
		if(getequippercentrefinery(.@part, REFINE_COST_NORMAL) < 100) {
			mes "[" + .@npc_name$ + "]";
			mes "那我要继续精炼它了哦?";
			mes "这真是一个有难度的挑战啊啊啊!";
			switch(.@material) {
			case 985:
				mes "^ff0000看来这装备已经精炼很多次了的样子...";
				mes "要是再继续精炼的话,";
				mes "我怕搞不好可能会造成装备损坏喔!!!^000000";
				break;
			default:
				mes "^ff0000要是损坏的话可就不能继续使用了!!";
				mes "插在里面的卡片和附魔也^0000ff全部消失^ff0000!!";
				mes "装备也会消失掉哦, 你确定要继续吗?";
				break;
			}
			next;
			mes "["+getarg(0)+"]";
			mes "我要再说一次, 一旦装备损坏了,";	
			mes "那么它就永远消失, 不能再使用了.";
			mes "也就是说, 你面临着一个选择";
			mes "^FF0000继续的话, 可能会永远失去这个装备^000000 .";
			mes "你真的确定要继续吗?";
			next;
			if(select("继续吧, 想变厉害总要承担风险的!:我伤不起, 算了...") == 2){
				mes "[" + .@npc_name$ + "]";
				mes "我完全理解你放弃的原因...";
				mes "哪怕我就是一个好的精炼师,";
				mes "但也会有偶尔出错的时候...";
				close;
			}
		}
		if((countitem(.@material) < 1) || (Zeny < .@price)) {
			mes "[" + .@npc_name$ + "]";
			mes "你没有足够的金钱或者 ^0000ff"+getitemname(.@material)+"^000000...";
			mes "回去拿一些来, 我会在这里等你的.";
			close;
		}
		Zeny = Zeny-.@price;
		delitem .@material,1;

		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) ||
		    callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt)) {
			mes "["+ .@npc_name$ +"]";
			emotion ET_FRET;
			mes "等一会儿..";
			mes "你以为我是笨蛋吗?!";
			mes "快出去, 不然我会用我的铁锤打你..";
			close;
		}

		if(getequippercentrefinery(.@part, REFINE_COST_NORMAL) <= rand(100)) {
			failedrefitem .@part;
			mes "["+ .@npc_name$ +"]";
			emotion (!rand(5))?ET_MONEY:ET_HUK;
			.@lose = rand(1,3);
			if (.@lose == 1) {
				mes "哦! 我的天!! 它真的损坏了...";
				mes "对此我真的十分非常抱歉,";
				mes "不过多多练习对我有好处不是么?";
			} else if(.@lose == 2) {
				mes "咳咳咳咳咳咳咳!!!";
				mes "装备在精炼的过程中损坏了.";
				mes "对...对不起...";
			} else {
				mes "砰! 啊啊啊啊~~ ";
				mes "装备在精炼的过程中损坏了.";
				mes "很抱歉, 真的十分抱歉...";
			}
			close;
		}
		mes "["+getarg(0)+"]";
		successrefitem .@part;
		emotion ET_SMILE;
		.@win = rand(1,3);
		if (.@win == 1) {
			mes "太棒了! 哈哈!";
			mes "要不要再来一次呀~~";
			mes "好久没精炼出这么优质的装备了,";
			mes "装备变强了你一定也很高兴吧?";
		} else if(.@win == 2) {
			mes "非常好, 成功了..!";
			mes "这是你的装备, 拿去吧!";
			mes "好久没精炼出这么优质的装备了.";
		} else {
			mes "哈哈! 我完成了.";
			mes "不要怀疑我的工作能力.";
			mes "肯定可以保证你满意.";
			mes "装备变强了你一定也很高兴吧?";
		}
		close;
	}

// New Refining Functions ========================
	if (.@refinerycnt < .@safe) {
		mes "[" + .@npc_name$ + "]";
		mes "你可以选择直接锻冶到安全值，或是要锻冶的次数... 我都没意见......";
		next;
		.@menu2 = select("锻冶到安全值.","让我来指定锻冶的次数.","我改变主意了...");
	} else
		.@menu2 = 2;
	switch(.@menu2){
	case 1: 
		.@refinecnt = .@safe - .@refinerycnt;
		break;
	case 2:
		next;
		mes "[" + .@npc_name$ + "]";
		mes "那么，你要我帮你锻冶多少次呢?";
		next;
		input .@refinecnt;
		.@refinecheck = .@refinecnt + .@refinerycnt;
		if (.@refinecnt < 1 || .@refinecheck > 10) {
			mes "[" + .@npc_name$ + "]";
			mes "我不能再精练这个物品啦.";
			close;
		}
		if(.@refinecheck > .@safe) {
			.@refinecheck = .@refinecheck - .@safe;
			mes "[" + .@npc_name$ + "]";
			mes "那么，我要开始锻冶  + .@refinecheck +  次，这将超过安定值. 你的装备有可能会坏掉... 要锻冶吗?";
			next;
			if(select("好的...","算了...") == 2){
				mes "[" + .@npc_name$ + "]";
				mes "这可是你说的哦...";
				close;
			}
		}
		break;
	case 3:
		next;
		mes "[" + .@npc_name$ + "]";
		mes "这可是你说的哦...";
		close;
	}
	.@fullprice = .@price * .@refinecnt;
	mes "[" + .@npc_name$ + "]";
	mes "总共需要" + .@refinecnt + " " + getitemname(.@material) + " 以及费用 " + .@fullprice + " Zeny... 要锻冶吗?";
	next;
	if(select("好的","算了...") == 2){
		mes "[" + .@npc_name$ + "]";
		mes "这可是你说的哦...";
		close;
	}
	if(countitem(.@material) < .@refinecnt || Zeny < .@fullprice) {
		mes "[" + .@npc_name$ + "]";
		mes "这些就是你所拥有的所有物品吗?虽然很可惜，但是你的材料不足，我也实在爱莫能助，再说，我付出也应该得要有所收获嘛!";
		close;
	}
	Zeny = Zeny - .@fullprice;
	delitem .@material,.@refinecnt;
	while(.@refinecnt){
		if (getequipisequiped(.@part) == 0) {
			mes "[" + .@npc_name$ + "]";
			mes "对不起,材料不够我也不能帮你.....";
			close;
		}
		// anti-hack
		if (callfunc("F_IsEquipIDHack", .@part, .@refineitemid) || callfunc("F_IsEquipCardHack", .@part, .@card[0], .@card[1], .@card[2], .@card[3]) ||
				callfunc("F_IsEquipRefineHack", .@part, .@refinerycnt) || (.@menu2 == 1 && getequippercentrefinery(.@part, REFINE_COST_NORMAL) < 100)) {
			mes "[" + .@npc_name$ + "]";
			mes "不要开玩笑了,你以为我是笨蛋吗 !! !?!";
			mes "快出去...";
			mes "不然我会用我的铁锤打你..!!!";
			close;
		} 
		mes "哐哐, 哐哐!!!";
		if(.@menu2 == 2 && getequippercentrefinery(.@part, REFINE_COST_NORMAL) <= rand(100)) {
			failedrefitem .@part;
			emotion ET_HUK;
			mes "[" + .@npc_name$ + "]";
			mes "咳咳咳咳咳咳咳!!! 武器在精炼的过程中损坏了...";
			.@refinecnt = .@refinecnt - 1;
			if(.@refinecnt == 0) close;
			mes "这是剩下的钱和材料...";
			getitem .@material,.@refinecnt;
			.@fullprice = .@refinecnt * .@price;
			Zeny = Zeny + .@fullprice;
			close;
		}
		successrefitem .@part;
		emotion ET_BEST;
		.@refinecnt = .@refinecnt - 1;
		.@refinerycnt = getequiprefinerycnt(.@part);
		next;
	}
	mes "["+ .@npc_name$ +"]";
	mes "所有的都完成了，欢迎下次再来.";
	close;
}

// Material Salesmen
//============================================================
prt_in,56,68,5	script	普勒越	86,{
	callfunc "phramain","普勒越";
	end;
}
payon,145,178,3	script	伯克纳德	88,{
	callfunc "phramain","伯克纳德";
	end;
}
morocc_in,63,32,6	script	沙德	99,{
	callfunc "phramain","沙德";
	end;
}
alberta_in,13,71,3	script	卡拉曼莉斯	86,{
	callfunc "phramain","卡拉曼莉斯";
	end;
}
yuno_in01,171,27,4	script	德雷曼	88,{
	callfunc "phramain","德雷曼";
	end;
}
ein_in01,15,87,3	script	迪乐意哈斯	86,{
	callfunc "phramain","迪乐意哈斯";
	end;
}
lhz_in02,278,24,3	script	客勒柯	86,{
	callfunc "phramain","客勒柯";
	end;
}

// Material Salesmen Functions
//============================================================
function	script	phramain	{
	if (checkweight(1201,1) == 0) {
		mes "你身上带着太多种东西了,";
		mes "去整理一下后再来找我吧!";
		close;
	}
	.@npc_name$ = getarg(0);
	mes "[" + .@npc_name$ + "]";
	mes "你好, 冒险家!";
	mes "我这里有卖一些铁或是钢铁制物品在精炼时所需要的金属.";
	next;
	mes "[" + .@npc_name$ + "]";
	mes "目前贩卖的东西有:";
	mes "精炼等级1武器的^d78400强化武器金属-级数一^000000.";
	mes "锻冶等级2武器的^007777强化武器金属-级数二^000000.";
	mes "请问你想要购买哪种?";
	next;
	switch(select("强化武器金属-级数一 - ^f56925200 金币^000000:强化武器金属-级数二 - ^f569251000 金币^000000:询问其他材料")) {
	case 1:
		.@material = 1010;
		.@price = 200;
		break;
	case 2:
		.@material = 1011;
		.@price = 1000;
		break;
	case 3:
		mes "[" + .@npc_name$ + "]";
		mes "其他材料?";
		mes "你需要精炼高等级的武器?";
		mes "我想铝和神之金属是很难找到的啦...";
		close;
	}
	mes "[" + .@npc_name$ + "]";
	mes "你想要购买多少个?";
	mes "如果你不想购买, 请输入数字 0 即可.";
	next;
	while(1) {
		input .@input;
		if (.@input <= 0) {
			mes "[" + .@npc_name$ + "]";
			mes "好的, 本次交易, 已经结束.";
			close;
		}
		else if (.@input < 0 || .@input > 500) {
			mes "[" + .@npc_name$ + "]";
			mes "好的, 咦....?";
			mes "你不可以一次购买超过 500 个, OK?";
			next;
		}
		else {
			break;
		}
	}
	.@sell = .@input * .@price;
	if (Zeny < .@sell) {
		mes "[" + .@npc_name$ + "]";
		mes "实在对不起哦.";
		mes "你现在的金币不足以购买这些东西啊.";
		mes "这里可不许赊帐的.";
		mes ""+ .@input +" of them.";
		close;
	}
	if (checkweight(.@material,.@input) == 0) {
		mes "[" + .@npc_name$ + "]";
		mes "很抱歉!";
		mes "您的负重无法购买那么多东西.";
		mes "请整理一下背包后再来吧.";
		close;
	}
	getitem .@material,.@input;
	Zeny = Zeny-.@sell;
	mes "[" + .@npc_name$ + "]";
	mes "好嘞, 这是你要的东西, 给你!";
	mes "非常感谢你的光顾.";
	close;
}

// Ori/Elu Refiners
//============================================================
prt_in,63,69,3	script	帝特立西	84,{
	callfunc "orimain","帝特立西";
	end;
}
payon,137,178,5	script	夏金	88,{
	callfunc "orimain","夏金";
	end;
}
morocc_in,72,32,6	script	亚度拉	99,{
	callfunc "orimain","亚度拉";
	end;
}
alberta_in,21,63,5	script	智诺普	84,{
	callfunc "orimain","智诺普";
	end;
}
yuno_in01,164,27,4	script	德雷伊	88,{
	callfunc "orimain","德雷伊";
	end;
}
ein_in01,18,82,6	script	马帝修塔音	84,{
	callfunc "orimain","马帝修塔音";
	end;
}
lhz_in02,281,24,5	script	葡鲁依	84,{
	callfunc "orimain","葡鲁依";
	end;
}

// Ori/Elu Functions
//============================================================
function	script	orimain	{
	if (checkweight(1201,1) == 0) {
		mes "你身上带着太多种东西了,";
		mes "去整理一下后再来找我吧!";
		close;
	}
	.@npc_name$ = getarg(0);
	mes "[" + .@npc_name$ + "]";
	mes "你好, 冒险家!";
	mes "只要你将^FF0000神之金属原石^000000或^FF0000铝原石^000000拿来.";
	mes "我马上就可以为你提炼^0000ff神之金属^000000和^0000ff铝^000000.";
	mes "但是记住, 必须要有 5 个原石，";
	mes "我才有办法帮你提炼.";
	next;
	switch(select("我想要制作^0000ff 神之金属 ^000000!:请帮我提炼出一块^ff0000 铝 ^000000!:询问关于属性矿石...")) {
	case 1:
		if (countitem(756) > 4) {
			delitem 756,5;  //Oridecon_Stone
			getitem 984,1; // Oridecon
			mes "[" + .@npc_name$ + "]";
			mes "这是我历尽千辛提炼出来的神之金属.";
			mes "欢迎你下次再来.";
			close;
		}
		else {
			mes "[" + .@npc_name$ + "]";
			mes "哈哈哈...你真爱开玩笑.";
			mes "我不是跟你说过, 要是没有5个神之金属原石的话, 是无法制作神之金属的吗?";
			close;
		}
	case 2:
		if (countitem(757) > 4) {
			delitem 757,5;  //Elunium_Stone
			getitem 985,1; // Elunium
			mes "[" + .@npc_name$ + "]";
			mes "这是我历尽千辛提炼出来的铝.";
			mes "欢迎你下次再来。";
			close;
		}
		else {
			mes "[" + .@npc_name$ + "]";
			mes "哈哈哈...你真爱开玩笑.";
			mes "我不是跟你说过, 要是没有5个铝原石的话, 是无法制作铝的吗?";
			close;
		}
	case 3:
		mes "[" + .@npc_name$ + "]";
		mes "关于属性矿石...";
		mes "我做锻冶金属这行已经做了二十年, 听过很多这类别的传说...";
		mes "但还没实际见过呢!";
		next;
		mes "[" + .@npc_name$ + "]";
		mes "我听说金属都各自拥有一些类似";
		mes "水、地、火、风等属性.";
		next;
		mes "[" + .@npc_name$ + "]";
		mes "在锻冶武器时加入属性时的话...";
		mes "所制作出来的武器, 将拥有那属性矿石的超强力量.";
		next;
		mes "[" + .@npc_name$ + "]";
		mes "哈哈..不过话说回来, ";
		mes "能做出这种武器的人想必一定拥有极高的锻冶技术.";
		close;
	}
}

// Equipment Repairmen
//============================================================
alberta_in,31,65,4	script	修理工#alb	86,{
	callfunc "repairmain","修理工";
	end;
}

moc_ruins,107,94,4	script	修理工#moc	99,{
	callfunc "repairmain","修理工";
	end;
}

payon,143,165,4	script	修理工#pay	88,{
	callfunc "repairmain","修理工";
	end;
}

prt_in,63,54,2	script	修理工#prt	86,{
	callfunc "repairmain","修理工";
	end;
}

yuno_in01,175,28,3	script	修理工#juno	86,{
	callfunc "repairmain","修理工";
	end;
}

geffen_in,34,166,3	script	修理工#gef	99,{
	callfunc "repairmain","修理工";
	end;
}

aldeba_in,38,60,3	script	修理工#alde	86,{
	callfunc "repairmain","修理工";
	end;
}

lhz_in02,284,14,3	script	修理工#lhz	86,{
	callfunc "repairmain","修理工";
	end;
}

prt_gld,139,117,4	script	修理工#prt_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

gef_fild13,263,117,4	script	修理工#gef_fild	86,{
	callfunc "repairmain","修理工";
	end;
}

pay_gld,295,183,4	script	修理工#pay_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

alde_gld,220,152,4	script	修理工#alde_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

aru_gld,189,336,4	script	修理工#aru_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

sch_gld,340,80,7	script	修理工#sch_gld	86,{
	callfunc "repairmain","修理工";
	end;
}

// Equipment Repair Function
//============================================================
function	script	repairmain	{
	.@repairprice = 5000;
	.@npc_name$ = getarg(0);
	mes "["+.@npc_name$+"]";
	mes "你好, 冒险家!";
	mes "你想让我修理东西吗?";
	next;
	mes "["+.@npc_name$+"]";
	mes "我可以帮你修理坏掉的装备及武器!";
	next;
	switch(select("恩...有东西修...:目前没有要修理的东西...")) {
	case 1:
		.@checkitem = 1;
		while (1) {
			if (getbrokenid(.@checkitem) == 0) {
				break;
			}
			.@checkitem = .@checkitem+1;
		}
		.@checkitem = .@checkitem-1;
		if (!.@checkitem) {
			mes "["+.@npc_name$+"]";
			mes "噢! 这真是难以置信.";
			mes "你肯定非常关心你的物品! 没有任何损坏哦~";
			next;
			mes "["+.@npc_name$+"]";
			mes "如果每个人都像你这样爱护物品的话, 我就会要失业了!";
			close;
		}
		mes "["+.@npc_name$+"]";
		mes "呃... 让我看看...";
		mes "你一共有 ^ff0000" + .@checkitem + "^000000 件物品损坏了.";
		mes "想修理他们吗?";
		next;
		.@totalcost = .@repairprice*.@checkitem;
		mes "["+.@npc_name$+"]";
		mes "修理一件需要 " + .@repairprice + " 金币.";
		mes "想修好全部物品需要 " + .@totalcost + " 金币!";
		mes "你想修理他们吗?";
		next;
		switch(select("是的, 帮我把它们都修理了吧.:算了, 暂时不修理了...")) {
		case 1:
			if (Zeny < .@totalcost) {
				mes "["+.@npc_name$+"]";
				mes "呃呃..";
				mes "你的金钱不足以支付维修费用啊.";
				mes "凑够钱再来找我吧.";
				close;
			}
			.@checkitem2 = 1;
			while (1) {
				if (getbrokenid(.@checkitem2) == 0) {
					break;
				}
				.@checkitem2 = .@checkitem2+1;
			}
			.@checkitem2 = .@checkitem2-1;
			if (.@checkitem == .@checkitem2) {
				Zeny = Zeny-.@totalcost;
				while (.@checkitem) {
					repair(.@checkitem);
					.@checkitem = .@checkitem-1;
				}
				mes "["+.@npc_name$+"]";
				mes "Ok! 全都帮你修好了.";
				close;
			}
			else {
				mes "["+.@npc_name$+"]";
				mes "看来你并没有任何坏掉的东西需要修理呐...";
				close;
			}
		case 2:
			mes "["+.@npc_name$+"]";
			mes "收费真的不算贵,";
			mes "客官你别走啊...";
			mes "我能帮你修理坏掉的装备及武器!";
			close;
		}
	case 2:
		mes "["+.@npc_name$+"]";
		mes "哦, 好的.";
		mes "等你需要修理物品的话,";
		mes "可以来找我.";
		close;
	}
}

//============================================================ 
// Old changelog
//============================================================ 
//= 1.0 by A bunch of people!
//=     Syrus22 - Completely redid the script using functions... also
//=     added the option for auto safe refining and multiple refining.
//= 1.1 Negative input bug fixed [Lupus]
//= 1.2 Added additional reparimen in Morocc and payon. Added
//=     Christopher the blacksmith in Geffen. Edited some dialogue [kobra_k88]
//= 1.3 New Payon Locations [Darkchild]
//=     Corrected zeny subtraction thx to jpnmania77.[kobra_k88]
//= 1.3a Temporary corrected an exploit. Need to check sources
//=     to fully fix bug [Shinigami]
//=     Fixed repairman prices [shadowlady]
//=     Fixed bug that skips requirements thanks to sir_loon [massdriller]
//=     Fixed itemid error thanks to -Vitamin- [massdriller]
//= 1.4 check again item in refining procedure to avoid
//=     hacker that can change item [dafide18]
//= 1.5 Fixed crashing due to badly used callfunc's [Skotlex]
//=     Lupus, don't rollback this important fix again! >.<
//= 1.5a Corrected an unneeded callfunc, fixed the anti-bot 
//=     exploit ruining the safe refine loop. [Skotlex]
//= 1.5b Fixed Spelling mistakes. [Nexon] 
//= 1.6 Replaced all breaks for ends as per the new script engine [Skotlex]
//= 1.7 Added Einbroch Refiners (Custom names ^^;) and a duplicated BS Shop. [Poki#3]
//= 1.8 Added Lighthalzen Refiners (Custom names again ^^;) [Poki#3]
//= 1.8a Fixed wrong indication thanks to NeoSaro [Lupus]
//= 1.9 Rewrote repairman, removed the Steel from repair cost [DracoRPG]
//= 2.0 Fixed missed equppment presence check. Thx2 Coltaro [Lupus]
//= 2.0a Added weight checks thanks to Neouni [Playtester]
//= 2.0b Fixed the names of Lighthalzen and Einbroch refiners thanks to Maud_Dib [Kargha]
//= 2.1 Removed Duplicates [Silent]
//= 2.2 Changed name from "Emvertacon" to "Emveretarcon". [Samuray22]
//=     Thanks to Barron-Monster.
//= 2.2b Changed name from "Pharacon" to "Phracon". [Samuray22]
//=     Thanks to Barron-Monster.
//= 2.3 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 2.4 Updated Refiner function. cleaner, and less dated. [L0ne_w0lf]
//= 2.5 Rather large update to the refiner and merchants. :D [L0ne_W0lf]
//= 2.6 Fixed a few bugs with creating pure stones. [L0ne_W0lf]
//= 2.7 Refiner function accepts additional paramater. [L0ne_W0lf]
//=     0 = No special features; 1 = new refining features
//=     Updated Repairmen and function. No longer shows menu.
//= 2.7a A couple touch-ups to the repairman function. [L0ne_w0lf]
//= 2.8 Changed the nonexistent variable .@matname$ for getitemname(.@material). (bugreport:2340) [Samuray22]
//= 2.8 Added proper Blacksmith Supplier to Einroch. [L0ne_W0lf]
//=     Updated dated features comment to reflect new usage.
//= 2.8a Small bugfix. (bugreport:2418) [Paradox924X]
//= 2.9 Moved Morocc repairman to Morocc Ruins. [L0ne_W0lf]
//============================================================
